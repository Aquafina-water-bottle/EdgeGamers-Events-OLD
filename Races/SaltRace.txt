//#-p stonebrick 0 -print
//471 7 -27

DEFINE $SA$ x=419,y=4,z=-47,dx=79,dy=40,dz=32
DEFINE $server$ minecraft:
DEFINE $Name$ Salt
DEFINE $Style$ 10 10 0

DEFINE Deactivate fill |0| stonebrick 0
DEFINE Activate fill |0| redstone_block 0

//Race Status Number
DEFINE $RSn$ 15
//Warp name
DEFINE $WN$ saltr
//Scoreboard teams color
DEFINE $Color$ gray
//Place where air is when open
DEFINE $Dao$ 468 6 -31 468 6 -35
//Place where redstone blocks are there when open
DEFINE $Dro$ 
//Spawn Coords
DEFINE $SC$ 467 8 -33 -90 0


//Checkpoint number
DEFINE $CPn$ 3
//Spawn area
DEFINE $Spn$ x=460,y=7,z=-35,dx=8,dy=3,dz=4

//Checkpoints for detection of players
DEFINE $CPdp1$ x=489,y=10,z=-34,dx=2,dy=3,dz=2
DEFINE $CPdp2$ x=450,y=15,z=-44,dx=2,dy=3,dz=2
DEFINE $CPdp3$ x=422,y=11,z=-34,dx=2,dy=4,dz=2

//Checkpoint teleports
DEFINE $CPtp0$ 472 8 -33 -90 20
DEFINE $CPtp1$ 490 11 -33 180 20
DEFINE $CPtp2$ 451 16 -43 90 20
DEFINE $CPtp3$ 423 12 -33 -90 20


:MainStart PULSE $Style$
Deactivate(MainStart)
//Scoreboard
scoreboard objectives add $Name$Race dummy $Name$ Race
scoreboard objectives add $Name$RaceCP dummy $Name$Race Checkpoints
scoreboard objectives setdisplay sidebar $Name$Race
scoreboard teams add $Name$R
scoreboard teams option $Name$R color $Color$
scoreboard teams option $Name$R seeFriendlyInvisibles true
scoreboard teams option $Name$R collisionRule never

//Race Status
$Name$R_Open RS = $RSn$
scoreboard players reset $Name$R_Closed RS

//Countdown startup
summon AreaEffectCloud ~ ~ ~ {Duration:101,Tags:["$Name$RaceCD"]}
tellraw @a[$SA$] {"text":"5","color":"yellow","bold":"true"}
@a[$SA$] playsound block.note.pling voice @a[c=1] ~ ~ ~ 0.5

//Other
effect @a[$SA$] clear
\/warp_$WN$ $Name$Race = 0
scoreboard teams join $Name$R /warp_$WN$
Activate(MainClock)
Activate(Countdown)


:MainClock CLOCK $Style$
$server$weather clear 1000000
gamemode 2 @a[$SA$,m=0]
$server$clear @a[$SA$,m=2]
scoreboard teams join $Name$R @a[$SA$,team=!$Name$R]
effect @a[$SA$,m=2,team=$Name$R] 14 3 0 true


//Checkpoint setups
@a[$Spn$,m=2,team=$Name$R] $Name$RaceCP + 0
LOOP 1 $CPn$ + 1 () 0 SUB($CPn$;1) + 1
	@a[$CPdp|0|$,m=2,team=$Name$R,$Name$RaceCP=|2|] playsound minecraft:entity.player.levelup voice @a[c=1]
	@a[$CPdp|0|$,m=2,team=$Name$R,$Name$RaceCP=|2|] tellraw @a[$SA$] {"text":"","extra":[{"selector":"@a[c=1,r=0,m=2,team=$Name$R,$Name$RaceCP=|2|]"},{"text":" has reached checkpoint |0|!","color":"yellow"}]}
	@a[$CPdp|0|$,m=2,team=$Name$R,$Name$RaceCP=|2|] $Name$RaceCP = |0|
END


//Teleports
@a[$SA$,m=2] hay_block -1 ~ ~-1.3 ~ scoreboard players tag @a[r=0,c=1] add SaltWater
scoreboard players tag @a[$Spn$,m=2,$Name$RaceCP>=1] add SaltWater
LOOP 0 $CPn$ + 1
	$server$tp @a[$SA$,m=2,SaltWater,$Name$RaceCP=|0|] $CPtp|0|$
END
effect @a[SaltWater] 6 1 100 true
effect @a[SaltWater] 11 1 100 true
@a[SaltWater] playsound minecraft:entity.endermen.teleport voice @a[c=1]
scoreboard players tag @a[SaltWater] remove SaltWater


:Countdown CLOCK $Style$
testfor @e[type=AreaEffectCloud,$Name$RaceCD] {Age:20}
CONSTART
	@a[$SA$] playsound minecraft:block.note.pling voice @a[c=1] ~ ~ ~ 1
	tellraw @a[$SA$] {"text":"4","color":"yellow","bold":"true"}
CONEND
testfor @e[type=AreaEffectCloud,$Name$RaceCD] {Age:40}
CONSTART
	@a[$SA$] playsound minecraft:block.note.pling voice @a[c=1] ~ ~ ~ 1.5
	tellraw @a[$SA$] {"text":"3","color":"yellow","bold":"true"}
CONEND
testfor @e[type=AreaEffectCloud,$Name$RaceCD] {Age:60}
CONSTART
	@a[$SA$] playsound minecraft:block.note.pling voice @a[c=1] ~ ~ ~ 2
	tellraw @a[$SA$] {"text":"2","color":"yellow","bold":"true"}
CONEND
testfor @e[type=AreaEffectCloud,$Name$RaceCD] {Age:80}
CONSTART
	@a[$SA$] playsound minecraft:block.note.pling voice @a[c=1] ~ ~ ~ 2
	tellraw @a[$SA$] {"text":"1","color":"yellow","bold":"true"}
CONEND
testfor @e[type=AreaEffectCloud,$Name$RaceCD] {Age:100}
CONSTART
	@a[$SA$] playsound minecraft:entity.wither.death voice @a[c=1]
	tellraw @a[$SA$] {"text":"GO!","color":"green","bold":"true"}
	title @a[$SA$] title {"text":"GO!","color":"green"}
	fill $Dao$ stonebrick 0 replace redstone_block 0
//	fill $Dro$ redstone_block 0 replace stonebrick 0
	Deactivate(Countdown)
CONEND


:MainEnd PULSE $Style$
Deactivate(MainEnd)
scoreboard objectives remove $Name$Race
scoreboard objectives remove $Name$RaceCP
scoreboard teams remove $Name$R

Deactivate(MainClock)
Deactivate(Countdown)

$Name$R_Closed RS = $RSn$
scoreboard players reset $Name$R_Open RS

$server$kill @e[type=AreaEffectCloud,$Name$RaceCD]
fill $Dao$ redstone_block 0 replace stonebrick 0
//fill $Dro$ stonebrick 0 replace redstone_block 0

effect @a[$SA$,m=2] clear
$server$clear @a[$SA$,m=2]
effect @a[$SA$,m=2] 6 20 100 true
$server$tp @a[$SA$,m=2] $SC$










