//#-p stonebrick 0 -print

//Royal Rumble. Finally after all this time. :)
//-129 3 399
//fill -113 3 399 -96 3 416 stonebrick 0

//Horses:
//W: summon EntityHorse ~-0.2 ~-0.4 ~ {ActiveEffects:[{Id:14b,Duration:10000000,ShowParticles:0b}],Rotation:[90f,0f],NoAI:1,NoGravity:1,Invulnerable:1,Silent:1,PersistenceRequired:1,Tags:["RRHorse"]}
//E: summon EntityHorse ~0.2 ~-0.4 ~ {ActiveEffects:[{Id:14b,Duration:10000000,ShowParticles:0b}],Rotation:[270f,0f],NoAI:1,NoGravity:1,Invulnerable:1,Silent:1,PersistenceRequired:1,Tags:["RRHorse"]}
//N: summon EntityHorse ~ ~-0.4 ~-0.2 {ActiveEffects:[{Id:14b,Duration:10000000,ShowParticles:0b}],Rotation:[180f,0f],NoAI:1,NoGravity:1,Invulnerable:1,Silent:1,PersistenceRequired:1,Tags:["RRHorse"]}
//S: summon EntityHorse ~ ~-0.4 ~0.2 {ActiveEffects:[{Id:14b,Duration:10000000,ShowParticles:0b}],Rotation:[0f,0f],NoAI:1,NoGravity:1,Invulnerable:1,Silent:1,PersistenceRequired:1,Tags:["RRHorse"]}
//scoreboard teams join NoPush @e[tag=RRHorse,r=10]

//Class ID: 0 = stealth, 1 = warrior, 2 = archer, 3 = engineer

//Item loadouts:
//0:
//	Offhand:regular bow
//	0:		wooden knife
//	1:		reinforced bow
//	3:		wooden shovel
//	7:		rocket count
//	inventory 0: regular arrow
//	inventory 8: seeing stick
//1:
//2:
//3:




//Display:
//Point1%
//Point2%
//Green
//Blue
//Minutes
//Seconds

//Minutes and Seconds are yellow
//Green and blue are RRdg and RRdb
//Point1% and Point2% is depending on what it is:
//	0 to 50% + not during capture: blue
//	51 to 80% + not during capture: dark_aqua
//	81% to 99% + not during capture: aqua
//	During capture: flashes between what it was at and green
//	When captured: dark green
//	12% of the time has to have the point captured?
//	Confirm item loadouts
//	Make sure item loadouts work properly with arrows


//Blue door 2nd spawn
//Option for death timer, and include warp timer
//Proper startup
//Timer based on time: 20 mins
//	10 added minutes when the point is captured
//	Might change to 15 minutes + 10 mins
//Pause time option (gamerule) + Custom time setting (add or remove one minute)
//Team switch
//Randomizer
//Overtime
//Blindness arrows = 0 damage
//Properly tag engie and stealth if cos is in offhand

//DEFINE RIHead slot.armor.head |0|_helmet 1 0
//DEFINE RIChest slot.armor.chest |0|_chestplate 1 0
//DEFINE RILegs slot.armor.legs |0|_leggings 1 0
//DEFINE RIFeet slot.armor.feet |0|_boots 1 0




//< Universal Defines
DEFINE $server$ minecraft:
//Make $server$ / for single player, and "minecraft:" for multiplayer

DEFINE $mod$ 0.4
DEFINE $SA$ x=-153,y=0,z=299,dx=158,dy=110,dz=168
DEFINE $ObjA$ scoreboard objectives add
DEFINE $ObjR$ scoreboard objectives remove
//DEFINE $PigEggG$ spawn_egg 1 0 {HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"Dispenser Pig"},EntityTag:{id:"Pig",Invulnerable:1,CustomName:"Dispenser Pig - Green",CustomNameVisible:0,Tags:["RRPigDispenser","RRPig","RRPigGreen"],NoAI:1,DeathLootTable:"",ActiveEffects:[{Ambient:0b,ShowParticles:0b,Duration:1000000,Id:10b,Amplifier:1b}],Attributes:[{Name:"generic.knockbackResistance",Base:1}]},CanPlaceOn:["stone","grass","dirt","cobblestone","planks","sand","gravel","log","leaves","glass","sandstone","wool","tallgrass","yellow_flower","red_flower","stone_slab","brick_block","tnt","bookshelf","mossy_cobblestone","obsidian","torch","ice","netherrack","glowstone","stained_glass","stonebrick","chest","iron_bars","glass_pane","lilypad","redstone_lamp","wooden_slab","cobblestone_wall","stained_hardened_clay","stained_glass_pane","leaves2","log2","sea_lantern","prismarine","carpet","hardened_clay","double_plant","packed_ice","lapis_block","gold_block","iron_block","diamond_block","emerald_block","redstone_block","quartz_block","sandstone_stairs","coal_block","oak_stairs","stone_stairs","quartz_stairs","fence","ladder","stone_brick_stairs","nether_brick","wall_sign","standing_sign","iron_trapdoor","hay_block","double_stone_slab"]}
//DEFINE $PigEggB$ spawn_egg 1 0 {HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"Dispenser Pig"},EntityTag:{id:"Pig",Invulnerable:1,CustomName:"Dispenser Pig - Blue",CustomNameVisible:0,Tags:["RRPigDispenser","RRPig","RRPigBlue"],NoAI:1,DeathLootTable:"",ActiveEffects:[{Ambient:0b,ShowParticles:0b,Duration:1000000,Id:10b,Amplifier:1b}],Attributes:[{Name:"generic.knockbackResistance",Base:1}]},CanPlaceOn:["stone","grass","dirt","cobblestone","planks","sand","gravel","log","leaves","glass","sandstone","wool","tallgrass","yellow_flower","red_flower","stone_slab","brick_block","tnt","bookshelf","mossy_cobblestone","obsidian","torch","ice","netherrack","glowstone","stained_glass","stonebrick","chest","iron_bars","glass_pane","lilypad","redstone_lamp","wooden_slab","cobblestone_wall","stained_hardened_clay","stained_glass_pane","leaves2","log2","sea_lantern","prismarine","carpet","hardened_clay","double_plant","packed_ice","lapis_block","gold_block","iron_block","diamond_block","emerald_block","redstone_block","quartz_block","sandstone_stairs","coal_block","oak_stairs","stone_stairs","quartz_stairs","fence","ladder","stone_brick_stairs","nether_brick","wall_sign","standing_sign","iron_trapdoor","hay_block","double_stone_slab"]}

DEFINE Deactivate fill |0| stonebrick 0
DEFINE Activate fill |0| redstone_block 0
DEFINE RemoveTag scoreboard players tag @a[x=-153,y=17,z=299,dx=158,dy=100,dz=168,m=2,|0|] remove |0|
DEFINE TextStart {"text":"","extra":[{"text":"[","color":"gray","bold":true},{"text":"Royal ","color":"blue","bold":true},{"text":"Rumble","color":"green","bold":true},{"text":"]: ","color":"gray","bold":true},

DEFINE $SpawnG$ -64 17 431
DEFINE $SpawnB$ -138 26 330

DEFINE $TPPos1$ -61 38 426
DEFINE $TPPos2$ -56 22 443
DEFINE $TPPos3$ -14 28 452 90 0
DEFINE $TPPos4$ -14 28 400 90 0
DEFINE $TPPos5$ -12 34 362
DEFINE $TPPos6$ -85 20 379
DEFINE $TPPos7$ -14 25 326
DEFINE $TPPos8$ -69 49 354

DEFINE $TPGlass1$ -61 37 426
DEFINE $TPGlass2$ -56 21 443
DEFINE $TPGlass3$ -14 27 452
DEFINE $TPGlass4$ -14 27 400
DEFINE $TPGlass5$ -12 33 362
DEFINE $TPGlass6$ -85 19 379
DEFINE $TPGlass7$ -14 24 326
DEFINE $TPGlass8$ -69 48 354

DEFINE $TPRdst1$ -56 38 426
DEFINE $TPRdst2$ -56 23 440
DEFINE $TPRdst3$ -14 28 450
DEFINE $TPRdst4$ -11 28 400
DEFINE $TPRdst5$ -9 34 362
DEFINE $TPRdst6$ -85 20 375
DEFINE $TPRdst7$ -17 25 326
DEFINE $TPRdst8$ -73 49 354
//>

//<	Class specific + item defines
//* Universal items *//
DEFINE ItemRedstone slot.weapon.offhand redstone_block |0| 0 {HideFlags:127,CanDestroy:["minecraft:sand"],display:{Lore:["Can only be placed","on pistons"]},CanPlaceOn:["minecraft:sticky_piston"],ench:[{id:0,lvl:1}]}
DEFINE ItemGlassPaneG slot.hotbar.8 stained_glass_pane 1 5 {HideFlags:127,display:{Name:"You are on Green"},CanDestroy:["minecraft:sand"]}
DEFINE ItemGlassPaneB slot.hotbar.8 stained_glass_pane 1 3 {HideFlags:127,display:{Name:"You are on Blue"},CanDestroy:["minecraft:sand"]}


//* Stealth Items *//
DEFINE StealItemSpeed dye 1 12 {HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"Speed Boost",Lore:["Speed 4"]},ench:[{id:0,lvl:1}],AttributeModifiers:[{AttributeName:"generic.movementSpeed",Name:"anything",Amount:0.8,Operation:1,UUIDLeast:1,UUIDMost:1}]}
DEFINE StealItemJump dye 1 10 {HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"Jump Boost",Lore:["Jump Boost 3"]},ench:[{id:0,lvl:1}]}
DEFINE StealItemCloak carrot_on_a_stick 1 0 {Unbreakable:1,HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"Cloaking Device",Lore:["Lasts 7 seconds on full charge","Cannot use items while cloaked"]}}
DEFINE StealItemKnife golden_sword 1 32 {HideFlags:127,ench:[{id:16,lvl:47}],display:{Name:"Big Earner",Lore:["1 hit kills any engie","Gives 75% cloak and","a 5 second speed boost","if the victim was an engie"]}}
DEFINE StealItemPoison lingering_potion 1 0 {HideFlags:127,CanDestroy:["minecraft:sand"],Potion:"minecraft:poison",CustomPotionEffects:[{Id:19b,Amplifier:10b,Duration:0}],display:{Lore:["Area of Effect poison cloud","Lasts 10 seconds, radius of 3","Only affects the other team"]}}
DEFINE StealItemRestock carrot_on_a_stick 1 0 {Unbreakable:1,HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"Restocks Ammo",Lore:["Restores the potion","and the Big Earner","Takes 6s to restock","and given blindness"]}}

DEFINE StealItemHeadG slot.armor.head wool 1 5 {Unbreakable:1,display:{Name:"The most unbreakable wool block"}}
DEFINE StealItemHeadB slot.armor.head wool 1 3 {Unbreakable:1,display:{Name:"The most unbreakable wool block"}}
DEFINE StealItemBleach potion 1 0 {HideFlags:127,CanDestroy:["minecraft:sand"],Potion:"invisibility",CustomPotionEffects:[{Id:7b,Amplifier:100b,Duration:1000000}],display:{Name:"Bleach",Lore:["Don't drink this in real life","Seriously"]}}
//Bleach will most likely be retired because of both controversial reasons and not having enough room in the player's inventory


//* Warrior Items *//
DEFINE WarrItemSword iron_sword 1 0 {Unbreakable:1,HideFlags:127,CanDestroy:["minecraft:sand"],AttributeModifiers:[{AttributeName:"generic.movementSpeed",Name:"anything",Amount:-0.15,Operation:1,UUIDLeast:1,UUIDMost:1,Slot:mainhand},{AttributeName:"generic.attackSpeed",Name:"anything",Amount:-2.4,Operation:0,UUIDLeast:1,UUIDMost:1,Slot:mainhand},{AttributeName:"generic.attackDamage",Name:"anything",Amount:5,Operation:0,UUIDLeast:1,UUIDMost:1,Slot:mainhand}],display:{Lore:["Holding this will","make you slower"]}}
DEFINE WarrItemShieldG shield 1 10 {Unbreakable:1,HideFlags:127,CanDestroy:["minecraft:sand"],BlockEntityTag:{Patterns:[],Base:10}}
DEFINE WarrItemShieldB shield 1 12 {Unbreakable:1,HideFlags:127,CanDestroy:["minecraft:sand"],BlockEntityTag:{Patterns:[],Base:12}}

DEFINE WarrItemHead slot.armor.head iron_helmet 1 0 {Unbreakable:1,HideFlags:127}
DEFINE WarrItemChest slot.armor.chest iron_chestplate 1 0 {Unbreakable:1,HideFlags:127}
DEFINE WarrItemLegs slot.armor.legs iron_leggings 1 0 {Unbreakable:1,HideFlags:127}
DEFINE WarrItemFeet slot.armor.feet iron_boots 1 0 {Unbreakable:1,HideFlags:127}


//* Archer Items *//
DEFINE ArchItemBow bow 1 0 {Unbreakable:1,HideFlags:127,CanDestroy:["minecraft:sand"],ench:[{id:51,lvl:1}],display:{Name:"Regular bow",Lore:["Infinity 1"]}}
DEFINE ArchItemKnife wooden_sword 1 0 {Unbreakable:1,HideFlags:127,CanDestroy:["minecraft:sand"],AttributeModifiers:[{AttributeName:"generic.attackSpeed",Name:"anything",Amount:-1.5,Operation:0,UUIDLeast:1,UUIDMost:1,Slot:mainhand},{AttributeName:"generic.attackDamage",Name:"anything",Amount:1,Operation:0,UUIDLeast:1,UUIDMost:1,Slot:mainhand}],display:{Name:"Wooden Knife",Lore:["Normally shoots out","pixelized toys"]}}
DEFINE ArchItemReinforced bow 1 0 {Unbreakable:1,HideFlags:127,CanDestroy:["minecraft:sand"],ench:[{id:48,lvl:3}],display:{Name:"Reinforced bow",Lore:["Shoots explosive arrows","Power 3"]}}
DEFINE ArchItemStick stick 1 0 {Unbreakable:1,HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"The Scoping Stick",Lore:["Zooms in and makes you"," immobile when held"]},AttributeModifiers:[{AttributeName:"generic.movementSpeed",Name:"anything",Amount:-3,Operation:1,UUIDLeast:1,UUIDMost:1,Slot:mainhand}]}
DEFINE ArchItemRocket arrow |0| |1| {HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"Rocket(s)"}}
DEFINE ArchItemArrow arrow |0| |1| {HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"A Regular Arrow",Lore:["Magically disappears when","you use the reinforced bow"]}}

DEFINE ArchItemHeadG slot.armor.head leather_helmet 1 0 {Unbreakable:1,HideFlags:127,display:{color:2490112}}
DEFINE ArchItemHeadB slot.armor.head leather_helmet 1 0 {Unbreakable:1,HideFlags:127,display:{color:6724056}}
DEFINE ArchItemChest slot.armor.chest chainmail_chestplate 1 0 {Unbreakable:1,HideFlags:127}
DEFINE ArchItemLegs slot.armor.legs chainmail_leggings 1 0 {Unbreakable:1,HideFlags:127}
DEFINE ArchItemFeet slot.armor.feet chainmail_boots 1 0 {Unbreakable:1,HideFlags:127}


//* Engie Items *//
DEFINE EngieItemPickaxe diamond_pickaxe 1 0 {Unbreakable:1,HideFlags:127,ench:[{id:32,lvl:1}],CanDestroy:["minecraft:obsidian","minecraft:redstone_block","minecraft:sand"],AttributeModifiers:[{AttributeName:"generic.attackSpeed",Name:"anything",Amount:-3.0,Operation:0,UUIDLeast:1,UUIDMost:1,Slot:mainhand},{AttributeName:"generic.attackDamage",Name:"anything",Amount:3.5,Operation:0,UUIDLeast:1,UUIDMost:1,Slot:mainhand}],display:{Lore:["Breaks redstone blocks,","obsidian and sand","Efficiency 1"]}}
DEFINE EngieItemBow bow 1 0 {Unbreakable:1,HideFlags:127,CanDestroy:["minecraft:sand"],display:{Lore:["Shoots blindness arrows"]}}
DEFINE EngieItemPotion potion 1 0 {Potion:"water_breathing",HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"Potion of Teleportation",Lore:["Teleports you back to spawn"]}}
DEFINE EngieItemPickup carrot_on_a_stick 1 0 {Unbreakable:1,HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"Pickup",Lore:["Picks up in a 3 block radius","Cannot attack while holding the pig"]}}
DEFINE EngieItemDestruct carrot_on_a_stick 1 0 {Unbreakable:1,HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"Self Destruct",Lore:["8 second delay before","placing a new pig"]}}
DEFINE EngieItemEggG spawn_egg 1 0 {HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"Dispenser Pig",Lore:["Oink"]},EntityTag:{id:"Pig",Invulnerable:1,Health:4.0f,CustomName:"Dispenser Pig - Green",CustomNameVisible:0,Tags:["RRPigDispenser","RRPig","RRPigGreen"],NoAI:1,DeathLootTable:"",ActiveEffects:[{Ambient:0b,ShowParticles:0b,Duration:1000000,Id:10b,Amplifier:1b}],Attributes:[{Name:"generic.knockbackResistance",Base:1}]},CanPlaceOn:["stone","grass","dirt","cobblestone","planks","sand","gravel","log","leaves","glass","sandstone","wool","tallgrass","yellow_flower","red_flower","stone_slab","brick_block","tnt","bookshelf","mossy_cobblestone","obsidian","torch","ice","netherrack","glowstone","stained_glass","stonebrick","chest","iron_bars","glass_pane","lilypad","redstone_lamp","wooden_slab","cobblestone_wall","stained_hardened_clay","stained_glass_pane","leaves2","log2","sea_lantern","prismarine","carpet","hardened_clay","double_plant","packed_ice","lapis_block","gold_block","iron_block","diamond_block","emerald_block","redstone_block","quartz_block","sandstone_stairs","coal_block","oak_stairs","stone_stairs","quartz_stairs","fence","ladder","stone_brick_stairs","nether_brick","wall_sign","standing_sign","iron_trapdoor","hay_block","double_stone_slab"]}
DEFINE EngieItemEggB spawn_egg 1 0 {HideFlags:127,CanDestroy:["minecraft:sand"],display:{Name:"Dispenser Pig",Lore:["Oink"]},EntityTag:{id:"Pig",Invulnerable:1,Health:4.0f,CustomName:"Dispenser Pig - Blue",CustomNameVisible:0,Tags:["RRPigDispenser","RRPig","RRPigBlue"],NoAI:1,DeathLootTable:"",ActiveEffects:[{Ambient:0b,ShowParticles:0b,Duration:1000000,Id:10b,Amplifier:1b}],Attributes:[{Name:"generic.knockbackResistance",Base:1}]},CanPlaceOn:["stone","grass","dirt","cobblestone","planks","sand","gravel","log","leaves","glass","sandstone","wool","tallgrass","yellow_flower","red_flower","stone_slab","brick_block","tnt","bookshelf","mossy_cobblestone","obsidian","torch","ice","netherrack","glowstone","stained_glass","stonebrick","chest","iron_bars","glass_pane","lilypad","redstone_lamp","wooden_slab","cobblestone_wall","stained_hardened_clay","stained_glass_pane","leaves2","log2","sea_lantern","prismarine","carpet","hardened_clay","double_plant","packed_ice","lapis_block","gold_block","iron_block","diamond_block","emerald_block","redstone_block","quartz_block","sandstone_stairs","coal_block","oak_stairs","stone_stairs","quartz_stairs","fence","ladder","stone_brick_stairs","nether_brick","wall_sign","standing_sign","iron_trapdoor","hay_block","double_stone_slab"]}
DEFINE EngieItemArrow tipped_arrow |0| |1| {Potion:"blindness",CustomPotionEffects:[{Id:15b,Amplifier:0b,Duration:160}],HideFlags:127,display:{Name:"Arrows of Blindness",Lore:["Lasts 8 seconds"]}}

DEFINE EngieItemHeadG slot.armor.head leather_helmet 1 0 {Unbreakable:1,HideFlags:127,display:{color:2490112}}
DEFINE EngieItemHeadB slot.armor.head leather_helmet 1 0 {Unbreakable:1,HideFlags:127,display:{color:6724056}}
DEFINE EngieItemChestG slot.armor.chest leather_chestplate 1 0 {Unbreakable:1,HideFlags:127,display:{color:2490112}}
DEFINE EngieItemChestB slot.armor.chest leather_chestplate 1 0 {Unbreakable:1,HideFlags:127,display:{color:6724056}}
DEFINE EngieItemLegsG slot.armor.legs leather_leggings 1 0 {Unbreakable:1,HideFlags:127,display:{color:2490112}}
DEFINE EngieItemLegsB slot.armor.legs leather_leggings 1 0 {Unbreakable:1,HideFlags:127,display:{color:6724056}}
DEFINE EngieItemFeetG slot.armor.feet leather_boots 1 0 {Unbreakable:1,HideFlags:127,display:{color:2490112}}
DEFINE EngieItemFeetB slot.armor.feet leather_boots 1 0 {Unbreakable:1,HideFlags:127,display:{color:6724056}}

//* Stealth Cloak options *//
DEFINE $StealthCloakMax$ 280
DEFINE $StealthCloakDispenserRegen$ 1
DEFINE $StealthCloakRegen$ 2
DEFINE $StealthCloakRemove$ 2
DEFINE $StealthCloakSecond$ 40

//* Stealth Poison Cloud options *//
DEFINE $StealthPoisonRadius$ 4

//* Stealth Big Earner options *//
DEFINE $StealthKnifeCloak$ 210
DEFINE $StealthKnifeSpeedDuration$ 5
DEFINE $StealthKnifeSpeedLevel$ 1

//* Stealth Restocking options *//
DEFINE $StealthRestockDelay$ 151


//* Archer Arrow Explosion Radius options *//
DEFINE $ArcherExplosionNear$ 2
DEFINE $ArcherExplosionFar$ 4
DEFINE $ArcherExplosionSelfNear$ 2
DEFINE $ArcherExplosionSelfFar$ 4


//* Engie Regeneration options *//
DEFINE $EngieRegenRadius$ 4
DEFINE $EngieRegenDelay$ 60

//* Engie Teleport Delay *//
DEFINE $EngieTeleportDelay$ 30

//* Dispenser options *//
DEFINE $DispenserRadius$ 3
DEFINE $DispenserRegenDelay$ 30
DEFINE $DispenserRocketDelay$ 80
DEFINE $DispenserBlindnessDelay$ 140



//>

//< General Scoreboards
FUNC GeneralScoreboardStart
	USE $ObjA$
		RR dummy Royal Rumble
		RRpa dummy RR Armor Stand
		RRci dummy RR Class ID
		RRpi dummy RR Player ID
		RRcs stat.useItem.minecraft.carrot_on_a_stick RR Carrot Stick
		RRde deathCount RR Deaths
		RRki playerKillCount Kills
		RRaf dummy RR Stats Affected Items
		RRcp dummy Capture Points
	UNUSE
	scoreboard teams add RRdg RR Display Green
	scoreboard teams add RRdb RR Display Blue
	scoreboard teams add RRg RR Green
	scoreboard teams add RRb RR Blue
	scoreboard teams add RRdk RR Dark Green
	scoreboard teams option RRdg color green
	scoreboard teams option RRdb color blue
	scoreboard teams option RRdg collisionRule never
	scoreboard teams option RRdb collisionRule never
	scoreboard teams option RRg color green
	scoreboard teams option RRb color blue
	scoreboard teams option RRg nametagVisibility hideForOtherTeams
	scoreboard teams option RRb nametagVisibility hideForOtherTeams
	scoreboard teams option RRg friendlyfire false
	scoreboard teams option RRb friendlyfire false
	
	//Change collisionRule if bug MC-87984 is fixed
	
	scoreboard teams option RRg collisionRule pushOwnTeam
	scoreboard teams option RRb collisionRule pushOwnTeam
	gamerule keepInventory true
	gamerule naturalRegeneration false
	scoreboard objectives setdisplay sidebar RR
	scoreboard objectives setdisplay list RRci
	
	summon Item ~ ~ ~ {Item:{id:"minecraft:stone",Count:1b},Age:6000s,Passengers:[
	//{id:ArmorStand,Invulnerable:1,NoGravity:1,Invisible:1,Marker:1b,Tags:["RRPoint1","RRPoint"]},
	//{id:ArmorStand,Invulnerable:1,NoGravity:1,Invisible:1,Marker:1b,Tags:["RRPoint2","RRPoint"]},
	//{id:ArmorStand,Invulnerable:1,NoGravity:1,Invisible:1,Marker:1b,Tags:["RRPointColor1","RRPoint"]},
	//{id:ArmorStand,Invulnerable:1,NoGravity:1,Invisible:1,Marker:1b,Tags:["RRPointColor2","RRPoint"]}
	//]}
END

FUNC GeneralScoreboardClock
	$server$weather clear 1000000
	$server$clear @a[$SA$,m=0]
	effect @a[$SA$,m=0] clear
	$server$gamemode 2 @a[$SA$,m=0]
	$server$kill @e[$SA$,type=Item]
	
	//Changing classes
	testfor @a[$SA$,m=2,c=1,RRTarget0] 
	CONDITION
		Activate(StealthItems)
	testfor @a[$SA$,m=2,c=1,RRTarget1]
	CONDITION
		Activate(WarriorItems)
	testfor @a[$SA$,m=2,c=1,RRTarget2]
	CONDITION
		Activate(ArcherItems)
	testfor @a[$SA$,m=2,c=1,RRTarget3]
	CONDITION
		Activate(EngineerItems)
	
	//Player ID
	@a[$SA$,m=2,RRpi=0,c=1] PlayerNumber RRpi + 1
	scoreboard players operation @a[$SA$,m=2,RRpi=0,c=1] RRpi = PlayerNumber RRpi
	
	//Point capture
	//scoreboard players add @e[type=ArmorStand,RRPoint1,RRcp<=2000] RRcp 1
	//scoreboard players add @e[type=ArmorStand,RRPoint2,RRcp<=2000] RRcp 1
	
	//scoreboard teams join RRdg Point1
	//scoreboard teams join RRdg Point1
	
	//RRPoint1 RRcp = @e[type=ArmorStand,RRPoint1] RRcp
	//RRPoint2 RRcp = @e[type=ArmorStand,RRPoint2] RRcp
	//RRPointColor1 RRcp = @e[type=ArmorStand,RRPoint1] RRcp
	//RRPointColor2 RRcp = @e[type=ArmorStand,RRPoint2] RRcp
	//RRPoint RRcp / 20
	
	//@e[type=ArmorStand,RRPoint1,RRcp=2000] fill -88 24 315 -88 18 345 obsidian 0 replace bedrock 0
	//@e[type=ArmorStand,RRPoint1,RRcp=2000] tellraw @a {"text":"The first point has been captured!","color":"green","bold":"true"}
	//@e[type=ArmorStand,RRPoint2,RRcp=2000] tellraw @a {"text":"The second point has been captured!","color":"green","bold":"true"}
	//@e[type=ArmorStand,RRPoint,RRcp=2000] RRcp = 9001
	
	
	
	//-88 24 315 -88 18 345 is obsidian wall
	//-40 28 339 -44 25 335 lapis point 1
	//-115 19 328 -119 22 332 lapis point 2
	
END

FUNC GeneralScoreboardEnd
	USE $ObjR$
		RR
		RRpa
		RRci
		RRpi
		RRcs
		RRde
		RRki
		RRaf
		RRcp
	UNUSE
	scoreboard teams remove RRg
	scoreboard teams remove RRb
	scoreboard teams remove RRdg
	scoreboard teams remove RRdb
	scoreboard teams remove RRdk
	$server$kill @e[type=ArmorStand,tag=RRPoint]
	$server$kill @e[$SA$,type=Arrow]
	scoreboard objectives setdisplay list Health
	gamerule keepInventory false
	gamerule naturalRegeneration true
	Deactivate(MainClock)
	Deactivate(Particle)
	Deactivate(SimpleParticle)
	Deactivate(DispenserPigAsthetics)
END
//>

//< Particle Clock
FUNC ParticleClock
	@e[type=ArmorStand,RRTPOpenP] RRpa + 1
	@e[type=ArmorStand,RRTPOpenP,RRpa>=72] RRpa = 1
	LOOP 1 72 + 1 () 15 1080 + 15.0 () 195 1260 + 15.0 () 5 360 + 5.0
		//say Tick |0| Angle |2| OppAngle |4| YAngle |6|
		@e[type=ArmorStand,RRTPOpenP,RRpa=|0|] 'particle happyVillager ~'MUL(SIN(|2|);$mod$)' ~'ADD(MUL(SIN(|6|);1.5);1.5)' ~'MUL(COS(|2|);$mod$)' 0 0 0 0 0 force'
		@e[type=ArmorStand,RRTPOpenP,RRpa=|0|] 'particle happyVillager ~'MUL(SIN(|4|);$mod$)' ~'ADD(MUL(SIN(|6|);1.5);1.5)' ~'MUL(COS(|4|);$mod$)' 0 0 0 0 0 force'
	END
END
//>

//< Teleporters
FUNC TeleportStart
	scoreboard objectives add RRTPID dummy RR Armor Stand
	LOOP 1 8 + 1
		@e[type=ArmorStand,RRTeleport|0|] RRTPID = |0|
	END
	scoreboard players tag @e[type=ArmorStand,RRTeleporter,r=1000] remove RRTPOff
	scoreboard players tag @e[type=ArmorStand,RRTeleporter,r=1000] remove RRTPOn
END

FUNC TeleportClock
	//Activated by tag "RRTPOn" done to the RRParticle armor stand. Open defined with "RRTPOpen".
	//Once activated, does playsound, tellraw, join team to blue, setblock glass
	//Avaliable Tags: RRTeleport#, RRParticle, RRTPOn, RRTPOpen, RRArmorStand, RRTeleporter, RRTeleport
	//Deactivated by RRTPOff
	LOOP 1 8 + 1
		@e[type=ArmorStand,tag=RRTPOn,RRTPID=|0|,r=1000] setblock $TPGlass|0|$ stained_glass 5
	END
	
	@e[type=ArmorStand,tag=RRTPOn,r=1000] playsound minecraft:block.lava.extinguish voice @a[r=10,team=RRg] ~ ~ ~ 2 0.5
	testfor @e[type=ArmorStand,tag=RRTPOn,r=1000]
		CONSTART
			@e[type=ArmorStand,tag=RRTPOn,r=1000] ~ ~-1 ~-1 scoreboard teams join RRdg @e[type=ArmorStand,tag=RRTeleporter,dy=2,dz=3]
			tellraw @a[r=1000] {"text":"","extra":[{"selector":"@e[type=ArmorStand,tag=RRTPOn]"},{"text":" has been enabled!","color":"gray"}]}
			@e[type=ArmorStand,tag=RRTPOn,r=1000] ~ ~-1 ~-1 scoreboard players tag @e[type=ArmorStand,tag=RRTeleporter,dy=2,dz=3] add RRTPOpen
			scoreboard players tag @e[type=ArmorStand,tag=RRTPOn,r=1000] add RRTPOpenP
		CONEND
	scoreboard players tag @e[type=ArmorStand,tag=RRTPOn,r=1000] remove RRTPOn
	
	@e[type=ArmorStand,RRTPOpen] scoreboard players tag @a[m=2,r=1,team=RRg] add RRTeleportEffect
	LOOP 1 8 + 1
		@e[type=ArmorStand,RRTPOpen,RRTPID=|0|] $server$tp @a[m=2,r=1,team=RRg] $TPPos|0|$
	END
	@a[$SA$,m=2,RRTeleportEffect] particle happyVillager ~0.1 ~1 ~0.1 0.3 0.5 0.3 1 20 force
	@a[$SA$,m=2,RRTeleportEffect] playsound minecraft:entity.endermen.teleport voice @a[c=1]
	scoreboard players tag @a[$SA$,m=2,RRTeleportEffect] remove RRTeleportEffect

	
	//Deactivation
	LOOP 1 8 + 1
		@e[type=ArmorStand,tag=RRTPOff,RRTPID=|0|,r=1000] setblock $TPGlass|0|$ stained_glass 3
	END
	
	@e[type=ArmorStand,tag=RRTPOff,r=1000] playsound minecraft:entity.item.pickup voice @a[r=10,team=RRg] ~ ~ ~ 1 0.5
	testfor @e[type=ArmorStand,tag=RRTPOff,r=1000]
		CONSTART
			@e[type=ArmorStand,tag=RRTPOff,r=1000] ~ ~-1 ~-1 scoreboard teams join RRdb @e[type=ArmorStand,tag=RRTeleporter,dy=2,dz=3]
			tellraw @a[r=1000] {"text":"","extra":[{"selector":"@e[type=ArmorStand,tag=RRTPOff]"},{"text":" has been disabled!","color":"gray"}]}
			@e[type=ArmorStand,tag=RRTPOff,r=1000] ~ ~-1 ~-1 scoreboard players tag @e[type=ArmorStand,tag=RRTeleporter,dy=2,dz=3] remove RRTPOpen
			scoreboard players tag @e[type=ArmorStand,tag=RRTPOff,r=1000] remove RRTPOpenP
		CONEND
	scoreboard players tag @e[type=ArmorStand,tag=RRTPOff,r=1000] remove RRTPOff

END

FUNC TeleportEnd
	scoreboard objectives remove RRTPID
	scoreboard players tag @e[type=ArmorStand,RRTeleporter,r=1000] remove RRTPOpen
	scoreboard players tag @e[type=ArmorStand,RRTeleporter,r=1000] remove RRTPOpenP
	LOOP 1 8 + 1
		setblock $TPGlass|0|$ stained_glass 3
		setblock $TPRdst|0|$ air 0
	END
	
END
//>

//< Armor Stand Particles
FUNC ArmorStandParticle
	//Armor Stand Reset, for asthetics. Also counting horses for chairs.
	$server$kill @e[type=ArmorStand,tag=RRArmorStand,r=1000]

	//Particles
	LOOP (-62.43 14 421.6;-64.43 14 421.6;-66.43 14 421.6;-68.43 14 421.6;-68.4 14 415.6;-66.4 14 415.6;-64.4 14 415.6;-62.4 14 415.6) 1 8 + 1
		summon ArmorStand |0| {Tags:["RRArmorStand","RRParticle","RRTeleporter","RRTeleport|1|"],CustomName:"Teleporter |1|",DisabledSlots:2096896,NoGravity:1,Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1b}
	END
	LOOP (-62.5 14.5 422.2;-62.5 15 422.2;-64.5 14.5 422.2;-64.5 15 422.2;-66.5 14.5 422.2;-66.5 15 422.2;-68.5 14.5 422.2;-68.5 15 422.2;-68.5 14.5 414.8;-68.5 15 414.8;-66.5 14.5 414.8;-66.5 15 414.8;-64.5 14.5 414.8;-64.5 15 414.8;-62.5 14.5 414.8;-62.5 15 414.8) (1;1;2;2;3;3;4;4;5;5;6;6;7;7;8;8)
		summon ArmorStand |0| {Tags:["RRArmorStand","RRTeleport","RRTeleporter","RRTeleport|1|"],DisabledSlots:2096896,NoGravity:1,Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1b}
	END
END
//>

//< Universal Class Change
FUNC UniversalGive
//Heals, clears
	$server$clear @a[$SA$,m=2,|0|]
	effect @a[$SA$,m=2,|0|] clear
	effect @a[$SA$,m=2,|0|] 6 1 100 true
//Sets scoreboards to reduce lag in clocks
	@a[$SA$,m=2,|0|] RRrs = 0
	@a[$SA$,m=2,|0|] RRaf + 0
	@a[$SA$,m=2,|0|] RRpi + 0
	@a[$SA$,m=2,|0|] RRar + 0
	@a[$SA$,m=2,|0|] RRrg + 0
	@a[$SA$,m=2,|0|] RRao + 0
	stats entity @a[$SA$,m=2,|0|] set AffectedItems @a[r=1,c=1] RRaf
//Glass pane
	replaceitem entity @a[$SA$,m=2,|0|,team=RRg] ItemGlassPaneG
	replaceitem entity @a[$SA$,m=2,|0|,team=RRb] ItemGlassPaneB
END
//>

//* Classes *//
//< Stealth
FUNC StealthStart
	USE $ObjA$
		RRsa dummy RR Sneak Amount
		RRst dummy RR Sneak State
		RRse dummy RR Sneak End
		RRsc dummy RR Sneak Calculations
		RRis dummy RR Inventory Slot
		RRia dummy RR Inventory Save
		RRic dummy RR Inventory Calc
		RRrs dummy RR Restock Timer
		RRpo dummy RR Thrown Potion
		RRur stat.useItem.minecraft.redstone_block RR Used Redstone
		RRup stat.useItem.minecraft.lingering_potion RR Used Lingering Potion
		RRuk stat.useItem.minecraft.golden_sword RR Used Big Earner
	UNUSE
END

//<	StealthCloakClock
FUNC StealthCloakClock
//(Features)
//	-Change slot: uncloak
//	-Currently lasts 7s
//	-Currently takes 7s to recharge
//	-Remembers if the player used the redstone block, knife and potion
//	-Does not remember inventory layout
//	-Takes 1s to decloak, with a notable noise + particle effect
//		-1s does not count for increase or decrease of the cloak: it stays as is until fully uncloaked
//		-Only given items after that 1s
//	-Cannot attack while cloaked, but can be attacked
//	-XP pickup sound is played every second only to the cloaked player (no one else can hear)

//Inventory Slot
	LOOP 0 8 + 1
		@a[m=2,RRci=0] RRis = |0| {SelectedItemSlot:|0|}
	END
	
//Setting tags for holding cloak + restock
	scoreboard players tag @a[$SA$,m=2,RRci=0] add RRHoldCloak {SelectedItem:{id:"minecraft:carrot_on_a_stick",Count:1b,tag:{display:{Name:"Cloaking Device"}}}}
	scoreboard players tag @a[$SA$,m=2,RRci=0] add RRHoldCloak {Inventory:[{Slot:-106b,id:"minecraft:carrot_on_a_stick",Count:1b,tag:{display:{Name:"Cloaking Device"}}}]}
	scoreboard players tag @a[$SA$,m=2,RRci=0] add RRHoldRestock {SelectedItem:{id:"minecraft:carrot_on_a_stick",Count:1b,tag:{display:{Name:"Restocks Ammo"}}}}
	scoreboard players tag @a[$SA$,m=2,RRci=0] add RRHoldRestock {Inventory:[{Slot:-106b,id:"minecraft:carrot_on_a_stick",Count:1b,tag:{display:{Name:"Restocks Ammo"}}}]}
	
//Inventory save if holding cloak
	@a[$SA$,m=2,RRci=0,RRHoldCloak] RRia = RRis
	
//Startup timer, only sets state to 1 if holding CoS, state = 0, class = stealth, and used CoS
//Also including playsound
	@a[$SA$,m=2,RRci=0,RRHoldCloak,RRcs>=1,RRst=0] particle explode ~ ~1 ~ 0.3 0.6 0.3 0 30 force @a
	@a[$SA$,m=2,RRci=0,RRHoldCloak,RRcs>=1,RRst=0] playsound minecraft:entity.arrow.shoot voice @a[r=10] ~ ~ ~ 2 0.5
	@a[$SA$,m=2,RRci=0,RRHoldCloak,RRcs>=1,RRst=0] RRst = 1

//Decrease and increase
	@a[$SA$,m=2,RRci=0,RRst=1,RRsa>=1<=SUB(ADD($StealthCloakMax$;$StealthCloakRegen$);1)] RRsa - $StealthCloakRemove$
	@a[$SA$,m=2,RRci=0,RRst=0,RRsa>=-5<=SUB($StealthCloakMax$;1)] RRsa + $StealthCloakRegen$
	
//Change (detection of changed inventory slot)
	@a[$SA$,m=2,RRci=0,RRst=1] RRic = RRia
	@a[$SA$,m=2,RRci=0,RRst=1] RRic - RRis
	@a[$SA$,m=2,RRci=0,RRst=1,RRic<=-1] RRst = 2
	@a[$SA$,m=2,RRci=0,RRst=1,RRic>=1] RRst = 2

//Change (detection of running out)
	@a[$SA$,m=2,RRci=0,RRst=1,RRsa<=0] RRst = 2
	
//Ending stage, including giving items back <-- Moved to under Big Earner effect
//Starts at 0, ends at 20
	@a[$SA$,m=2,RRci=0,RRst=2,RRse>=0<=19] RRse + 1
	scoreboard players tag @a[$SA$,m=2,RRci=0,RRst=2,RRse=20] add RRStealthInvRestore
	
	@a[$SA$,m=2,RRci=0,RRst=2,RRse=20] RRst = 0
	@a[$SA$,m=2,RRci=0,RRst=0,RRse=20] RRse = 0
	
//Effects, clears
	@a[$SA$,m=2,RRci=0,RRst=2] particle smoke ~ ~1 ~ 0.3 0.5 0.3 0 10 force @a
	@a[$SA$,m=2,RRci=0,RRst=2] playsound entity.squid.death voice @a[r=10] ~ ~ ~ 0.6 0.5
	$server$clear @a[$SA$,m=2,RRci=0,RRst>=1<=2]
	effect @a[$SA$,m=2,RRci=0,RRst>=1<=2] 14 2 0 true
	effect @a[$SA$,m=2,RRci=0,RRst=0] 14 0 0 true
	
//XP effects
	$server$xp -10L @a[$SA$,m=2]
	$server$xp 1L @a[$SA$,m=2,RRci=0]
	$server$xp 1 @a[$SA$,m=2,RRci=0,score_RRsa_min=1]
	$server$xp 1 @a[$SA$,m=2,RRci=0,score_RRsa_min=40]
	$server$xp 1 @a[$SA$,m=2,RRci=0,score_RRsa_min=80]
	$server$xp 1 @a[$SA$,m=2,RRci=0,score_RRsa_min=120]
	$server$xp 1 @a[$SA$,m=2,RRci=0,score_RRsa_min=160]
	$server$xp 1 @a[$SA$,m=2,RRci=0,score_RRsa_min=200]
	$server$xp 1 @a[$SA$,m=2,RRci=0,score_RRsa_min=240]
	$server$xp 1 @a[$SA$,m=2,RRci=0,score_RRsa_min=280]
	
//XP Sound effects
//Plays every second
	@a[$SA$,m=2,RRst=1,RRci=0,RRsa>=4<=241] RRsc = RRsa
	@a[$SA$,m=2,RRst=1,RRci=0,RRsa>=4<=241] RRsc % $StealthCloakSecond$
	@a[$SA$,m=2,RRst=1,RRci=0,RRsc>=0<=1,RRsa>=4<=SUB($StealthCloakMax$;$StealthCloakSecond$)] playsound entity.experience_orb.touch voice @a[c=1] ~ ~ ~ 0.5
	@a[m=2,RRci=0,RRsc>=-1<=280] RRsc = 0
	
//Reset on Death
//	@a[RRde>=1,RRci=0] RRst = 0
//	@a[RRde>=1,RRci=0] RRsa = 0
//	@a[RRde>=1,RRci=0] RRse = 0

//Reset on spawn
//	@a[RRde>=1,RRci=0] RRup = 0
//	@a[RRde>=1,RRci=0] RRur = 0

	
//Other
	//Remove tag
	scoreboard players tag @a[$SA$,m=2] remove RRHoldCloak

END
//>

//<	StealthClock
FUNC StealthClock
	//Change from dropping firework charge to poison lingering potion with custom effects
	//RRup
	
	//Summon areaeffectclouds on thrown potion entity similarly to the snowball setup
	//if not killed, sets up areaeffectcloud
	
//* Adding to ThrownPotion entity *//
	@e[$SA$,type=ThrownPotion] RRpo + 0 {Potion:{id:"minecraft:lingering_potion",tag:{Potion:"minecraft:poison",CustomPotionEffects:[{Duration:0,Id:19b,Amplifier:10b}]}}}
	@a[$SA$,m=2,RRci=0,RRup=1,RRst=0,team=RRg] scoreboard players tag @e[r=5,type=ThrownPotion,RRpo=0] add RRThrownPotionG 
	@a[$SA$,m=2,RRci=0,RRup=1,RRst=0,team=RRb] scoreboard players tag @e[r=5,type=ThrownPotion,RRpo=0] add RRThrownPotionB 
	@a[RRup=1] RRup = 2
	@e[$SA$,type=ThrownPotion,RRpo=0] RRpo = 1
	@e[$SA$,type=ThrownPotion,RRpo=1,RRThrownPotionG] $server$kill @e[type=AreaEffectCloud,RRCloudTempG,r=2,c=1]
	@e[$SA$,type=ThrownPotion,RRpo=1,RRThrownPotionB] $server$kill @e[type=AreaEffectCloud,RRCloudTempB,r=2,c=1]
	@e[type=AreaEffectCloud,RRCloudTemp] RRpo + 0
	
//* Adding to poison cloud *//
	scoreboard players tag @e[type=AreaEffectCloud] add RRPoisonCloud {Effects:[{Ambient:0b,ShowParticles:1b,Duration:0,Id:19b,Amplifier:10b}]}
	@e[type=AreaEffectCloud,RRPoisonCloud] RRpo + 0
	
	@e[type=AreaEffectCloud,RRCloudTempG,RRpo=0] scoreboard players tag @e[type=AreaEffectCloud,RRPoisonCloud,RRpo=0,r=4,c=1] add RRPoisonCloudG
	@e[type=AreaEffectCloud,RRCloudTempB,RRpo=0] scoreboard players tag @e[type=AreaEffectCloud,RRPoisonCloud,RRpo=0,r=4,c=1] add RRPoisonCloudB
	entitydata @e[type=AreaEffectCloud,RRPoisonCloud,RRpo=0] {Radius:$StealthPoisonRadius$,RadiusPerTick:0f,Duration:200,RapplicationDelay:1000,RadiusOnUse:0f,WaitTime:0}
	@e[type=AreaEffectCloud,RRPoisonCloudG,RRpo=0] RRpo = 1
	@e[type=AreaEffectCloud,RRPoisonCloudB,RRpo=0] RRpo = 1
	$server$kill @e[type=AreaEffectCloud,RRCloudTemp,RRpo=0]
	
//* If all else fails *//
	scoreboard players tag @e[type=AreaEffectCloud,RRPoisonCloud,RRpo=0] add RRPoisonCloudG
	scoreboard players tag @e[type=AreaEffectCloud,RRPoisonCloud,RRpo=0] add RRPoisonCloudB
	@e[type=AreaEffectCloud,RRPoisonCloud,RRpo=0] RRpo = 1

//* Poison effects *//
//Cannot use poison anywhere else
//Make it poison pigs too
	@e[type=AreaEffectCloud,RRPoisonCloudG] scoreboard players tag @a[m=2,r=$StealthPoisonRadius$,team=RRb] add RRPoisoned
	@e[type=AreaEffectCloud,RRPoisonCloudG] scoreboard players tag @e[r=$StealthPoisonRadius$,type=Pig,RRPigBlue,RRns=3] add RRPoisoned
	@e[type=AreaEffectCloud,RRPoisonCloudB] scoreboard players tag @a[m=2,r=$StealthPoisonRadius$,team=RRg] add RRPoisoned
	@e[type=AreaEffectCloud,RRPoisonCloudB] scoreboard players tag @e[r=$StealthPoisonRadius$,type=Pig,RRPigGreen,RRns=3] add RRPoisoned
	effect @a[$SA$,m=2,RRPoisoned] 19 1 10 true
	effect @e[type=Pig,RRPoisoned,RRns=3] 19 1 10 true
	effect @a[$SA$,m=2,!RRPoisoned] 19 0 10 true
	effect @e[type=Pig,!RRPoisoned,RRns=3] 19 0 10 true
	scoreboard players tag @a[RRPoisoned] remove RRPoisoned
	scoreboard players tag @e[type=Pig,RRPoisoned] remove RRPoisoned
	@e[type=AreaEffectCloud,RRPoisonCloudG] particle mobSpell ~ ~ ~ 0.3 1 0.3 1 0 force
	@e[type=AreaEffectCloud,RRPoisonCloudB] particle mobSpell ~ ~ ~ 0.3 0.3 1 1 0 force
	

//* Summoning clouds on ThrownPotion entity *//
	@e[$SA$,type=ThrownPotion,RRpo=1,RRThrownPotionB] summon AreaEffectCloud ~ ~ ~ {Duration:3,Tags:["RRCloudTemp","RRCloudTempB"]}
	@e[$SA$,type=ThrownPotion,RRpo=1,RRThrownPotionG] summon AreaEffectCloud ~ ~ ~ {Duration:3,Tags:["RRCloudTemp","RRCloudTempG"]}
	
	
//* Jump boost *//
	scoreboard players tag @a[$SA$,m=2,RRci=0] add RRJumpBoost {SelectedItem:{id:"minecraft:dye",Count:1b,Damage:10s,tag:{display:{Name:"Jump Boost"}}}}
	scoreboard players tag @a[$SA$,m=2,RRci=0] add RRJumpBoost {Inventory:[{Slot:-106b,id:"minecraft:dye",Count:1b,Damage:10s,tag:{display:{Name:"Jump Boost"}}}]}
	effect @a[$SA$,RRci=0,m=2,!RRJumpBoost] 8 0 0 true
	effect @a[$SA$,RRci=0,m=2,RRJumpBoost] 8 1 2 true
	scoreboard players tag @a[RRJumpBoost] remove RRJumpBoost


//* Big Earner Effect *//
//When holding the sword: no weakness
	scoreboard players tag @a[RRci=0,RRuk=0] add RRHoldingBigEarner {SelectedItem:{id:"minecraft:golden_sword",tag:{display:{Name:"Big Earner"}}}}
	scoreboard players tag @a[RRci=0,RRuk=0,!RRHoldingBigEarner] add RRWeakness
	scoreboard players tag @a[RRHoldingBigEarner] remove RRHoldingBigEarner

//Detects when: engineer is within a 5 block radius and died, and the stealth person has killcount of 1 + usedsword of 1
	@a[RRci=3,RRde=1] scoreboard players tag @a[RRci=0,r=5,RRki=1,RRuk=1,c=1] add RRBigEarnerBuff
	@a[$SA$,RRci=0,RRBigEarnerBuff] RRsa + $StealthKnifeCloak$
	@a[$SA$,RRci=0,RRBigEarnerBuff,RRsa>=$StealthCloakMax$] RRsa = $StealthCloakMax$
	effect @a[$SA$,RRci=0,RRBigEarnerBuff] 1 $StealthKnifeSpeedDuration$ $StealthKnifeSpeedLevel$ true
	@a[RRuk=1] RRuk = 2
	@a[RRBigEarnerBuff] playsound minecraft:entity.player.levelup voice @a[RRBigEarnerBuff,c=1] ~ ~ ~ 2
	scoreboard players tag @a[RRBigEarnerBuff] remove RRBigEarnerBuff


//* Restocking effect *//
//Takes 6s, cannot be canceled, cannot attack while doing so
//During restocking: blindness, cleared inventory and cannot attack
	@a[$SA$,m=2,RRci=0,RRcs>=1,RRuk=2,RRrs=0,RRHoldRestock] RRrs = $StealthRestockDelay$
	@a[$SA$,m=2,RRci=0,RRcs>=1,RRup=2,RRrs=0,RRHoldRestock] RRrs = $StealthRestockDelay$
	@a[$SA$,m=2,RRci=0,RRrs=$StealthRestockDelay$] playsound minecraft:block.chest.open voice @a[r=5] ~ ~ ~ 2
	effect @a[$SA$,m=2,RRci=0,RRrs=$StealthRestockDelay$] 15 100 0 true
	effect @a[$SA$,m=2,RRci=0,RRrs=$StealthRestockDelay$] 2 100 1 true
	$server$clear @a[$SA$,m=2,RRci=0,RRrs=$StealthRestockDelay$]
	
	@a[$SA$,m=2,RRci=0,RRrs>=1<=$StealthRestockDelay$] RRrs - 1
	@a[$SA$,m=2,RRci=0,RRrs>=1<=$StealthRestockDelay$] particle endRod ~ ~1 ~ 0.5 0.8 0.5 0 1 force
	
//Canceled upon death
	@a[RRci=0,RRrs>=1,RRde>=1] RRrs = 0
	
//When RRrs score is at 1: restores inventory
	scoreboard players tag @a[$SA$,m=2,RRci=0,RRrs=1] add RRStealthInvRestore
	effect @a[$SA$,m=2,RRci=0,RRrs=1] 15 0 0 true
	effect @a[$SA$,m=2,RRci=0,RRrs=1] 2 0 0 true
	@a[$SA$,m=2,RRci=0,RRrs=1] playsound minecraft:entity.player.levelup voice @a[c=1] ~ ~ ~ 2
	@a[$SA$,m=2,RRci=0,RRrs=1] RRup = 0
	@a[$SA$,m=2,RRci=0,RRrs=1] RRuk = 0
	scoreboard players tag @a[RRHoldRestock] remove RRHoldRestock


//* Inventory restore after cloaking and restocking *//
	testfor @a[$SA$,m=2,RRStealthInvRestore]
	CONSTART
		replaceitem entity @a[$SA$,m=2,RRStealthInvRestore] slot.hotbar.0 StealItemSpeed
		replaceitem entity @a[$SA$,m=2,RRStealthInvRestore] slot.hotbar.1 StealItemJump
		replaceitem entity @a[$SA$,m=2,RRStealthInvRestore] slot.hotbar.2 StealItemCloak
		replaceitem entity @a[$SA$,m=2,RRStealthInvRestore] slot.hotbar.7 StealItemRestock
	CONEND
	
	replaceitem entity @a[$SA$,m=2,RRStealthInvRestore,team=RRg] StealItemHeadG
	replaceitem entity @a[$SA$,m=2,RRStealthInvRestore,team=RRb] StealItemHeadB
	replaceitem entity @a[$SA$,m=2,RRStealthInvRestore,team=RRg] ItemGlassPaneG
	replaceitem entity @a[$SA$,m=2,RRStealthInvRestore,team=RRb] ItemGlassPaneB
	
	replaceitem entity @a[$SA$,m=2,RRStealthInvRestore,RRur=0] ItemRedstone(1)
	replaceitem entity @a[$SA$,m=2,RRStealthInvRestore,RRup=0] slot.hotbar.4 StealItemPoison
	replaceitem entity @a[$SA$,m=2,RRStealthInvRestore,RRuk=0] slot.hotbar.5 StealItemKnife
	
	scoreboard players tag @a[RRStealthInvRestore] remove RRStealthInvRestore



	scoreboard players tag @a[m=2,RRde>=1,RRci=0] add RRTarget0	
END
//>

FUNC StealthEnd
	USE $ObjR$
		RRsa
		RRst
		RRse
		RRsc
		RRis
		RRia
		RRic
		RRrs
		RRpo
		RRur
		RRup
		RRuk
	UNUSE
END

FUNC StealthGive
	@a[$SA$,m=2,RRTarget0] RRci = 0
	@a[$SA$,m=2,RRTarget0] RRur = 0
	@a[$SA$,m=2,RRTarget0] RRup = 0
	@a[$SA$,m=2,RRTarget0] RRuk = 0
	@a[$SA$,m=2,RRTarget0] RRst = 0
	@a[$SA$,m=2,RRTarget0] RRse = 0
	@a[$SA$,m=2,RRTarget0] RRsa = $StealthCloakMax$
	replaceitem entity @a[$SA$,m=2,RRTarget0] slot.hotbar.0 StealItemSpeed
	replaceitem entity @a[$SA$,m=2,RRTarget0] slot.hotbar.1 StealItemJump
	replaceitem entity @a[$SA$,m=2,RRTarget0] slot.hotbar.2 StealItemCloak
	replaceitem entity @a[$SA$,m=2,RRTarget0] slot.hotbar.4 StealItemKnife
	replaceitem entity @a[$SA$,m=2,RRTarget0] slot.hotbar.5 StealItemPoison
	replaceitem entity @a[$SA$,m=2,RRTarget0] slot.hotbar.7 StealItemRestock
	replaceitem entity @a[$SA$,m=2,RRTarget0,team=RRg] StealItemHeadG
	replaceitem entity @a[$SA$,m=2,RRTarget0,team=RRb] StealItemHeadB
	replaceitem entity @a[$SA$,m=2,RRTarget0] ItemRedstone(1)
END

FUNC StealthCC
	@a[$SA$,m=2,|0|] RRst = 0
	@a[$SA$,m=2,|0|] RRsa = 0
	@a[$SA$,m=2,|0|] RRse = 0
END
//>





//< Warrior
FUNC WarriorStart
END
FUNC WarriorClock
	scoreboard players tag @a[m=2,RRde>=1,RRci=1] add RRTarget1
END
FUNC WarriorEnd
END
FUNC WarriorGive
	@a[$SA$,m=2,RRTarget1] RRci = 1
	replaceitem entity @a[$SA$,m=2,RRTarget1] WarrItemHead
	replaceitem entity @a[$SA$,m=2,RRTarget1] WarrItemChest
	replaceitem entity @a[$SA$,m=2,RRTarget1] WarrItemLegs
	replaceitem entity @a[$SA$,m=2,RRTarget1] WarrItemFeet
	replaceitem entity @a[$SA$,m=2,RRTarget1] slot.hotbar.0 WarrItemSword
	replaceitem entity @a[$SA$,m=2,RRTarget1,team=RRg] slot.weapon.offhand WarrItemShieldG
	replaceitem entity @a[$SA$,m=2,RRTarget1,team=RRb] slot.weapon.offhand WarrItemShieldB
END
FUNC WarriorCC
END
//>





//<Archer
FUNC ArcherStart
	USE $ObjA$
		RRbu stat.useItem.minecraft.bow RR Bow Use
		RRbi dummy RR Bow ID
		RRar dummy RR Arrows
		RRac dummy RR Arrow Calc
		RRac2 dummy RR Arrow Choose
	UNUSE
END

//<	ArcherClock
FUNC ArcherClock
	//RRar 0: adding
	//RRar 1: in air
	//RRar 2: in ground, adding
	//RRar 3: only goes to this score if it has the RRExplosiveArrow tag to prevent damage twice
	
	//RRar 1 for players: long range
	//RRar 0 for players: close range
	
//* Bow ID *//
//1 is holding reg, while 2 is reinforced. reset at end. Replaces "RRHoldingExplosives tag"
	@a[$SA$,m=2,RRci=2] RRbi = 1 {Inventory:[{Slot:-106b,id:"minecraft:bow",Count:1b,tag:{display:{Name:"Regular bow"}}}]}
	@a[$SA$,m=2,RRci=2] RRbi = 2 {Inventory:[{Slot:-106b,id:"minecraft:bow",Count:1b,tag:{display:{Name:"Reinforced bow"}}}]}
	@a[$SA$,m=2,RRci=2] RRbi = 1 {SelectedItem:{id:"minecraft:bow",Count:1b,tag:{display:{Name:"Regular bow"}}}}
	@a[$SA$,m=2,RRci=2] RRbi = 2 {SelectedItem:{id:"minecraft:bow",Count:1b,tag:{display:{Name:"Reinforced bow"}}}}


//* Regular arrow *//
	replaceitem entity @a[$SA$,m=2,RRci=2,RRbi=1] slot.inventory.0 ArchItemArrow(1;0)
	minecraft:clear @a[$SA$,m=2,RRci=2,RRbi=2] ArchItemArrow(-1;-1)


//* Tags arrows *//
	@e[$SA$,type=Arrow] RRar + 0
	@a[$SA$,m=2,RRci=2,RRbu>=1,RRbi=2] scoreboard players tag @e[type=Arrow,RRar=0,r=5,c=1] add RRExplosiveArrow {damage:4.0d}
	@a[$SA$,m=2,RRci=2,RRbu>=1,RRbi=2] scoreboard players operation @e[type=Arrow,RRar=0,r=5,c=1,RRExplosiveArrow] RRpi = @a[r=1,c=1,RRci=2] RRpi
	@a[$SA$,m=2,RRci=2,RRbu>=1,RRbi=2,team=RRg] scoreboard players tag @e[type=Arrow,RRar=0,r=5,RRExplosiveArrow] add RRExplosiveArrowG {damage:4.0d}
	@a[$SA$,m=2,RRci=2,RRbu>=1,RRbi=2,team=RRb] scoreboard players tag @e[type=Arrow,RRar=0,r=5,RRExplosiveArrow] add RRExplosiveArrowB {damage:4.0d}
	
	scoreboard players tag @e[type=Arrow,RRar=0] add RRBlindnessArrow {CustomPotionEffects:[{Id:15b,Amplifier:0b,Duration:160}]}
	entitydata @e[type=Arrow,RRar=0,RRBlindnessArrow] {damage:0.0d}
	
	@e[$SA$,type=Arrow,RRar=0] RRar = 1
	
//* Particle and playsound effects *//
	@e[$SA$,type=Arrow,RRar=1] RRar = 2 {inGround:1b}
	@e[type=Arrow,RRExplosiveArrow,RRar=2] playsound minecraft:entity.generic.explode voice @a ~ ~ ~ 1 0.75
	@e[type=Arrow,RRExplosiveArrow,RRar=2] particle hugeexplosion ~ ~ ~ 0 0 0 0 1 force
	
//* Damage dealt to other team setting *//
	//Requires both direct and splash damage
	//Get averages
	
	//Use RRar on players: 1 for far, 2 for near, 0 for neutral
	//radius of 2: near. 4: far
	//All resistance effects cleared here: change if necessary
	
	effect @a[$SA$,m=2] 11 0 0 true
	effect @e[type=Pig,RRPig] 11 0 0 true
	
//* Self inflicted damage setting *//
	//Use the pig pickup format but with chooses like self destuct
	//Also starts from an entity versus a player, which means all players - entity
	
	@e[type=Arrow,RRar=2,RRExplosiveArrow,c=1] RRar = 3
	
	@e[$SA$,type=Arrow,RRar=3,RRExplosiveArrowG] ~ ~-1 ~ @a[m=2,team=RRb,r=$ArcherExplosionFar$,RRar=0] RRar = 1
	@e[$SA$,type=Arrow,RRar=3,RRExplosiveArrowG] ~ ~-1 ~ @a[m=2,team=RRb,r=$ArcherExplosionNear$,RRar=1] RRar = 2
	@e[$SA$,type=Arrow,RRar=3,RRExplosiveArrowB] ~ ~-1 ~ @a[m=2,team=RRg,r=$ArcherExplosionFar$,RRar=0] RRar = 1
	@e[$SA$,type=Arrow,RRar=3,RRExplosiveArrowB] ~ ~-1 ~ @a[m=2,team=RRg,r=$ArcherExplosionNear$,RRar=1] RRar = 2
	@e[$SA$,type=Arrow,RRar=3,RRExplosiveArrowG] ~ ~-1 ~ @e[type=Pig,RRPigBlue,r=$ArcherExplosionFar$,RRar=0] RRar = 1
	@e[$SA$,type=Arrow,RRar=3,RRExplosiveArrowG] ~ ~-1 ~ @e[type=Pig,RRPigBlue,r=$ArcherExplosionNear$,RRar=1] RRar = 2
	@e[$SA$,type=Arrow,RRar=3,RRExplosiveArrowB] ~ ~-1 ~ @e[type=Pig,RRPigGreen,r=$ArcherExplosionFar$,RRar=0] RRar = 1
	@e[$SA$,type=Arrow,RRar=3,RRExplosiveArrowB] ~ ~-1 ~ @e[type=Pig,RRPigGreen,r=$ArcherExplosionNear$,RRar=1] RRar = 2
	
	@e[type=Arrow,RRar=3] ~ ~-1 ~ @a[m=2,RRci=2,r=$ArcherExplosionSelfFar$] RRac2 = 1
	@e[type=Arrow,RRar=3] ~ ~-1 ~ @a[m=2,RRci=2,r=$ArcherExplosionSelfNear$] RRac2 = 2
	
	@a[$SA$,m=2,RRci=2,RRac2>=1<=2] RRac = RRpi
	scoreboard players operation @a[$SA$,m=2,RRci=2,RRac2>=1<=2] RRac -= @e[type=Arrow,c=1,RRar=3] RRpi
	@a[m=2,RRci=2,RRac=0,RRar=0,RRac2>=1<=2] RRar = RRac2
//	@a[m=2,RRci=2,RRac=0,RRar=0,RRac2=1] RRar = 1
//	@a[m=2,RRci=2,RRac=0,RRar=1,RRac2=2] RRar = 2
	
//* Deals damage based on class and range *//
//		HrmLvl	ResLvl	Dmg		CN	CF
//		1		2		1.8			w
//		1		1		2.4		w	a
//		1		0		3		a	e
//		2		2		3.6		e	
//		2		1		4.8			s
//		2		0		6		s	
//Pig = archer

	effect @a[$SA$,m=2,RRci=0,RRar=1] 11 1 0 true
	effect @a[$SA$,m=2,RRci=0,RRar>=1<=2] 7 1 1 true
	
	effect @a[$SA$,m=2,RRci=3,RRar=2] 11 1 1 true
	effect @a[$SA$,m=2,RRci=3,RRar=2] 7 1 1 true
	effect @a[$SA$,m=2,RRci=3,RRar=1] 7 1 0 true
	
	effect @a[$SA$,m=2,RRci=2,RRar=1] 11 1 0 true
	effect @a[$SA$,m=2,RRci=1,RRar=2] 11 1 0 true
	effect @a[$SA$,m=2,RRci=1,RRar=1] 11 1 1 true
	effect @a[$SA$,m=2,RRci>=1<=2,RRar>=1<=2] 7 1 0 true
	
	effect @e[type=Pig,RRar=1,RRPig] 11 1 0 true
	effect @e[type=Pig,RRar>=1<=2,RRPig] 7 1 0 true
	
//* Reset all arrow scores and tags *//
	scoreboard players reset @a[RRac<=10000] RRac
	scoreboard players reset @a[RRac2<=10000] RRac2
	@a[RRar>=1] RRar = 0
	@e[type=Pig,RRar>=1] RRar = 0
	@a[RRbu>=1] RRbu = 0
	scoreboard players tag @e[type=Arrow,RRar=3] add RRCanKillArrow
	scoreboard players tag @e[$SA$,type=Arrow,RRar=2,!RRExplosiveArrow] add RRCanKillArrow
	$server$kill @e[type=Arrow,RRCanKillArrow]
	@a[RRbi>=1] RRbi = 0
	
	scoreboard players tag @a[m=2,RRde>=1,RRci=2] add RRTarget2
END
//>

FUNC ArcherEnd
	USE $ObjR$
		RRbu
		RRbi
		RRar
		RRac
		RRac2
	UNUSE
END
FUNC ArcherGive
	@a[$SA$,m=2,RRTarget2] RRci = 2
	@a[$SA$,m=2,RRTarget2] RRbu = 0
	replaceitem entity @a[$SA$,m=2,RRTarget2] slot.weapon.offhand bow 1 0 {Unbreakable:1,HideFlags:127,CanDestroy:["minecraft:sand"],ench:[{id:51,lvl:1}],display:{Name:"Regular bow",Lore:["Infinity 1"]}}
	replaceitem entity @a[$SA$,m=2,RRTarget2] slot.hotbar.0 ArchItemReinforced
	replaceitem entity @a[$SA$,m=2,RRTarget2] slot.hotbar.1 ArchItemKnife
	replaceitem entity @a[$SA$,m=2,RRTarget2] slot.hotbar.3 ArchItemStick
	replaceitem entity @a[$SA$,m=2,RRTarget2] slot.hotbar.7 ArchItemRocket(3;0)
	
	replaceitem entity @a[$SA$,m=2,RRTarget2,team=RRg] ArchItemHeadG
	replaceitem entity @a[$SA$,m=2,RRTarget2,team=RRb] ArchItemHeadB
	replaceitem entity @a[$SA$,m=2,RRTarget2] ArchItemChest
	replaceitem entity @a[$SA$,m=2,RRTarget2] ArchItemLegs
	replaceitem entity @a[$SA$,m=2,RRTarget2] ArchItemFeet
END
FUNC ArcherCC
END
//>





//<Engineer
FUNC EngineerStart
	USE $ObjA$
		RRam dummy RR Pig Amount
		RRns dummy RR Newly Spawned Pig
		RRue stat.useItem.minecraft.spawn_egg RR Used Spawn Egg
		RRdp dummy RR Dead Pig
		RRae dummy RR Stats Affected Entities
		RRtd dummy RR Teleport Delay
		RRpt dummy RR Pig State
		RRrt dummy RR Regen State
		RRrg dummy RR Regeneration
		RRao dummy RR Pig Ammo
		RRpc1 dummy RR Pig Calc 1
		RRpc2 dummy RR Pig Calc 2
		RRpc3 dummy RR Pig Calc 3
		RRpc4 dummy RR Pig XP Calc
		RRpc5 dummy RR Pig Calc 5
	UNUSE
END

//<	EngineerPigClock
FUNC EngineerPigClock
//	Given 3 states:
//	0: regular, can build
//	1: when built, between 0 and 2 (giving carrot on a stick)
//	2: when built
	
//Newly spawned, use RRpi as pig ID although not for pig, but for armor stand - Armorstand stores all info, while pig just has the display. If pig dies, is detected by the armor stand.
//Requires pig to be spawned not ontop of another pig for proper armor stand detection
//Requires baby pig detection and recovery
//Send requests via carrot on a stick. 1 request per tick will be processed

//Pig RRns:
//RRns 0: just spawned
//RRns 1: set after rotation
//RRns 2: set when on ground
//RRns 3: after all calculations on ground, ending
	
//Armor Stand RRns:
//RRns 0: just spawned
//RRns 2: immediately sets to 2 to avoid confusion as armor stands are summoned at RRns 2 stage of the pig
//RRns 3: ending

//* Tags for the player when holding certain weapons *//
	scoreboard players tag @a[$SA$,m=2,RRci=3] add RRPigPickup {SelectedItem:{id:"minecraft:carrot_on_a_stick",Count:1b,tag:{display:{Name:"Pickup"}}}}
	scoreboard players tag @a[$SA$,m=2,RRci=3] add RRPigDestruct {SelectedItem:{id:"minecraft:carrot_on_a_stick",Count:1b,tag:{display:{Name:"Self Destruct"}}}}
	
	
//* Setting up the dispenser *//
	@a[$SA$,m=2,RRci=3] RRpt + 0
	@e[$SA$,type=Pig,tag=RRPig] RRns + 0
	scoreboard teams join RRdb @e[$SA$,type=Pig,RRns=0,RRPigBlue]
	scoreboard teams join RRdg @e[$SA$,type=Pig,RRns=0,RRPigGreen]
	
//* Rotation of the pig *//
	@a[$SA$,m=2,RRci=3,RRpt=0,RRue=1,ry=224,rym=135] scoreboard players tag @e[type=Pig,RRPig,RRns=0,r=6,c=1] add RRPigRotationS
	@a[$SA$,m=2,RRci=3,RRpt=0,RRue=1,ry=44,rym=-45] scoreboard players tag @e[type=Pig,RRPig,RRns=0,r=6,c=1] add RRPigRotationN
	@a[$SA$,m=2,RRci=3,RRpt=0,RRue=1,ry=-44,rym=-135] scoreboard players tag @e[type=Pig,RRPig,RRns=0,r=6,c=1] add RRPigRotationW
	@a[$SA$,m=2,RRci=3,RRpt=0,RRue=1,ry=134,rym=45] scoreboard players tag @e[type=Pig,RRPig,RRns=0,r=6,c=1] add RRPigRotationE
	
//* Setting the PID of the pig *//
	@e[$SA$,type=Pig,RRPigBlue,RRns=0] RRpi = @a[c=1,r=6,team=RRb,RRci=3,RRpt=0,RRue>=1] RRpi
	@e[$SA$,type=Pig,RRPigGreen,RRns=0] RRpi = @a[c=1,r=6,team=RRg,RRci=3,RRpt=0,RRue>=1] RRpi
	@a[$SA$,m=2,RRci=3,RRue>=1] RRue = 2
//(Change so if the RRpi value of the pig is 0, it is automatically killed)
	@e[$SA$,type=Pig,RRns=0,RRPig] RRns = 1
	
//* Recovery for baby pig, with tellraw message *//
	scoreboard players tag @e[$SA$,type=Pig] add RRPigBaby {CustomName:"Dispenser Pig"}
	@e[$SA$,type=Pig,RRPigBaby] scoreboard players tag @a[r=6,RRci=3,RRpt=0,RRue=0] add RRPlacedBabyTest
	@a[$SA$,m=2,RRci=3,RRPlacedBabyTest] minecraft:clear @a[r=1,c=1] spawn_egg -1 0
	scoreboard players tag @a[$SA$,RRci=3,RRpt=0,RRaf=0,RRPlacedBabyTest] add RRPlacedBaby
	
	//Killing of baby pigs
	$server$kill @e[$SA$,type=Pig,RRPigBaby]
	scoreboard players tag @a[$SA$,m=2,RRci=3,RRPlacedBaby] add RRPigDispenserGive
	tellraw @a[$SA$,m=2,RRci=3,RRPlacedBaby] TextStart{"text":"Don't place a pig on another pig!","color":"red"}]}
	scoreboard players tag @a[RRPlacedBabyTest] remove RRPlacedBabyTest
	scoreboard players tag @a[RRPlacedBaby] remove RRPlacedBaby
	
	//Objective: RRns
	//Tags for all: RRPigDispenser
	//Tags: RRPig, RRPigBlue, RRPigGreen
	//Tags for AS: RRArmorStand, RRASPig, RRASPigGreen, RRASPigBlue
	
//OnGround set RRns to 2
	@e[$SA$,type=Pig,RRns=1,RRPig] RRns = 2 {OnGround:1b}
	
//* Detection of a pig being on another pig *//
//Closest player with RRue of 2 to pig with RRns of 2, RRae min of 2 and RRpt of 0
	stats entity @e[$SA$,type=Pig,RRPig,RRns=2] set AffectedEntities @e[r=1,c=1,type=Pig] RRae
	@e[$SA$,type=Pig,RRPig] RRae + 0
	@e[$SA$,type=Pig,RRPig] tellraw @e[type=Pig,RRPig,r=0] baby i'm ballin
	scoreboard players tag @e[$SA$,type=Pig,RRPig,RRae>=2,RRns=2] add RRPigPlaced
	@e[$SA$,type=Pig,RRPigPlaced] scoreboard players tag @a[r=6,c=1,RRue>=2,RRpt=0] add RRPlacedOnPig
	$server$kill @e[$SA$,type=Pig,RRPigPlaced]
	scoreboard players tag @a[$SA$,m=2,RRci=3,RRPlacedOnPig] add RRPigDispenserGive
	@a[$SA$,m=2,RRci=3,RRPlacedOnPig] RRue = 0
	tellraw @a[$SA$,m=2,RRci=3,RRPlacedOnPig] TextStart{"text":"Don't place a pig on top of another pig!","color":"red"}]}
	scoreboard players tag @a[RRPlacedOnPig] remove RRPlacedOnPig
	
	
//* Regular summoning of armor stands, removal of invulnerability from pigs *//
	@e[$SA$,type=Pig,RRns=2,RRPig] RRar + 0
	$server$tp @e[type=Pig,RRPigRotationN,RRns=2] ~ ~ ~ 180 0
	$server$tp @e[type=Pig,RRPigRotationS,RRns=2] ~ ~ ~ 0 0
	$server$tp @e[type=Pig,RRPigRotationE,RRns=2] ~ ~ ~ 270 0
	$server$tp @e[type=Pig,RRPigRotationW,RRns=2] ~ ~ ~ 90 0
	
	@e[$SA$,type=Pig,RRns=2,RRPigGreen] summon ArmorStand ~ ~ ~ {DisabledSlots:2096896,Marker:1,NoGravity:1,NoBasePlate:1,ShowArms:1,Small:1,Invulnerable:1,PersistenceRequired:1,Invisible:1,ArmorItems:[{},{},{},{id:stained_hardened_clay,Count:1b,Damage:5s}],Tags:["RRPigDispenser","RRArmorStand","RRASPig","RRASPigGreen"]}
	@e[$SA$,type=Pig,RRns=2,RRPigBlue] summon ArmorStand ~ ~ ~ {DisabledSlots:2096896,Marker:1,NoGravity:1,NoBasePlate:1,ShowArms:1,Small:1,Invulnerable:1,PersistenceRequired:1,Invisible:1,ArmorItems:[{},{},{},{id:stained_hardened_clay,Count:1b,Damage:3s}],Tags:["RRPigDispenser","RRArmorStand","RRASPig","RRASPigBlue"]}
	@e[$SA$,type=ArmorStand,RRASPig] RRns + 0
	@e[$SA$,type=ArmorStand,RRASPig,RRns=0] RRns = 2
	entitydata @e[$SA$,type=Pig,RRPig,RRns=2] {Invulnerable:0}
	
	@e[$SA$,type=ArmorStand,RRASPig,RRns=2] RRpi = @e[type=Pig,RRPig,c=1,dy=-1,RRns=2] RRpi
	
	
//* Detection of RRpt to go to 1 *//
//	RRpc5
	scoreboard players tag @e[$SA$,type=ArmorStand,RRASPig,c=1,RRns=2] add RRASPigRRptChoose
	@e[$SA$,type=ArmorStand,RRASPigRRptChoose] @a[m=2,RRci=3,RRpt=0,RRue=2] scoreboard players operation @a[m=2,RRci=3,RRpt=0,RRue=2,c=1] RRpc5 = @a[m=2,RRci=3,RRpt=0,RRue=2,c=1] RRpi
	scoreboard players operation @a[m=2,RRci=3,RRpt=0,RRue=2] RRpc5 -= @e[$SA$,type=ArmorStand,RRASPigRRptChoose] RRpi
	@a[m=2,RRci=3,RRpt=0,RRue=2,RRpc5=0] RRpt = 1
	
	@a[RRci=3,RRpt=1,RRue>=2] RRue = 0
	@e[type=Pig,RRPig,RRns=2] RRns = 3
	@e[type=ArmorStand,RRASPigRRptChoose,RRns=2] RRns = 3
	scoreboard players reset @a[RRpc5<=10000] RRpc5
	scoreboard players tag @e[type=ArmorStand,RRASPigRRptChoose] remove RRASPigRRptChoose
	
//* Transfer from RRpt 1 to RRpt 2 *//
	$server$clear @a[$SA$,m=2,RRci=3,RRpt=1] spawn_egg
	replaceitem entity @a[$SA$,m=2,RRci=3,RRpt=1] slot.hotbar.5 EngieItemPickup
	replaceitem entity @a[$SA$,m=2,RRci=3,RRpt=1] slot.hotbar.6 EngieItemDestruct
	replaceitem entity @a[$SA$,m=2,RRci=3,RRpt=1,RRDispenserPickup] slot.hotbar.1 EngieItemBow
	scoreboard players tag @a[$SA$,m=2,RRci=3,RRpt=1,RRDispenserPickup] remove RRDispenserPickup
	
	@a[m=2,RRpt=1,RRci=3] RRpt = 2
	
	
//* Request for pickup, will test to see if the target matches *//
//RRPickupRequest (person), RRPickupRequestTarget (armor stand)
	scoreboard players tag @a[$SA$,m=2,RRci=3,RRpt=2,RRcs>=1,RRPigPickup] add RRPickupRequest
	@a[$SA$,RRPickupRequest] scoreboard players tag @e[type=ArmorStand,c=1,r=3,RRASPig] add RRPickupRequestTarget
	
	@a[$SA$,RRPickupRequest] RRpc1 = RRpi
	@a[$SA$,RRPickupRequest] RRpc1 - @e[type=ArmorStand,RRPickupRequestTarget,c=1,r=3] RRpi
	@a[$SA$,RRPickupRequest,RRpc1=0] @e[type=ArmorStand,RRPickupRequestTarget,c=1,r=3] ~ ~1 ~ scoreboard players tag @e[type=Pig,RRPig,c=1,dy=-3] add RRPickupDeath
	@e[type=Pig,RRPickupDeath] particle explode ~ ~0.5 ~ 0.5 0.3 0.5 0 30 force
	@e[type=Pig,RRPickupDeath] playsound minecraft:entity.item.pickup voice @a[r=5]
	$server$tp @e[type=Pig,RRPickupDeath] ~ ~-150 ~
	$server$kill @e[type=Pig,RRPickupDeath]
	@a[$SA$,RRPickupRequest,RRpc1=0] $server$kill @e[type=ArmorStand,RRPickupRequestTarget,c=1,r=3]
	
//* If RRpc1 matches with a score of 0, activates all *//
	$server$clear @a[$SA$,RRPickupRequest,RRpc1=0] carrot_on_a_stick
	$server$clear @a[$SA$,RRPickupRequest,RRpc1=0] bow
	scoreboard players tag @a[$SA$,RRPickupRequest,RRpc1=0] add RRPigDispenserGive
	scoreboard players tag @a[$SA$,RRPickupRequest,RRpc1=0] add RRDispenserPickup
	scoreboard players tag @a[$SA$,m=2,RRDispenserPickup] add RRWeakness
	@a[$SA$,RRPickupRequest,RRpc1=0] RRpt = 0
	scoreboard players tag @a[$SA$,RRPickupRequest] remove RRPickupRequest
	scoreboard players reset @a[RRpc1<=10000] RRpc1
	scoreboard players tag @e[type=ArmorStand,RRPickupRequestTarget] remove RRPickupRequestTarget
	
//* If dead during pickup phaze *//
	scoreboard players tag @a[RRDispenserPickup,RRde>=1,RRci=3] add RRDeadPigPlayer
	scoreboard players tag @a[RRDispenserPickup,RRde>=1,RRci=3] remove RRDispenserPickup
	

//* Request for self destruct *//
	scoreboard players tag @a[$SA$,m=2,RRci=3,RRpt=2,RRcs>=1,RRPigDestruct] add RRDestructRequest
	scoreboard players tag @a[m=2,RRDestructRequest,c=1] add RRDestructRequestChoose
	@a[RRDestructRequestChoose,c=1] @e[RRPigDispenser] RRpc2 = RRpi
	scoreboard players operation @e[RRPigDispenser,RRpc2<=10000] RRpc2 -= @a[RRDestructRequestChoose,c=1] RRpi
	@e[type=ArmorStand,RRASPig,RRpc2=0] $server$kill @e[type=Pig,RRPig,c=1,dy=-1]
	$server$kill @e[RRPigDispenser,RRpc2=0]
	
//*  Clearing, replaceitem, etc. *//
	$server$clear @a[RRci=3,RRDestructRequestChoose] carrot_on_a_stick
	@a[RRDestructRequestChoose] RRpt = 0
	@a[RRDestructRequestChoose] RRam = 160
	@a[RRam>=1<=320,m=2] RRam - 1
	scoreboard players tag @a[m=2,RRci=3,RRam=0,RRpt=0] add RRPigDispenserGive
	@a[m=2,RRci=3,RRam=0,RRpt=0] playsound minecraft:entity.player.levelup voice @a[c=1]
	scoreboard players reset @a[RRam=0] RRam
	scoreboard players tag @a[RRDestructRequest] remove RRDestructRequest
	scoreboard players tag @a[RRDestructRequestChoose] remove RRDestructRequestChoose
	
//* Upon death of the dispenser *//
	@e[$SA$,type=ArmorStand,RRASPig] RRdp = 1
	@e[$SA$,type=Pig,RRPig] ~ ~-1 ~ @e[type=ArmorStand,RRASPig,dy=1] RRdp = 0
	@e[$SA$,type=ArmorStand,RRASPig,RRdp=1] $server$kill @e[type=Pig,RRPig,dy=-1]
	scoreboard players tag @e[$SA$,type=ArmorStand,RRASPig,RRdp=1,c=1] add RRDeadPigChoose
	@e[$SA$,type=ArmorStand,RRDeadPigChoose,RRdp=1,c=1] @a[m=2,RRci=3,RRpt=2] scoreboard players operation @a[c=1,m=2,RRci=3,RRpt=2] RRpc3 = @a[c=1,m=2,RRci=3,RRpt=2] RRpi
	scoreboard players operation @a[m=2,RRci=3,RRpt=2] RRpc3 -= @e[$SA$,type=ArmorStand,RRdp=1,RRDeadPigChoose] RRpi
	$server$kill @e[type=ArmorStand,RRdp=1,RRDeadPigChoose]
	scoreboard players tag @a[m=2,RRci=3,RRpt=2,RRpc3=0] add RRDeadPigPlayer
	@a[RRDeadPigPlayer,RRci=3] RRpt = 0
	@a[RRDeadPigPlayer,RRci=3] RRam = 320
	tellraw @a[RRDeadPigPlayer,RRci=3] TextStart{"text":"Your dispenser had been destroyed!","color":"red"}]}
	@a[RRDeadPigPlayer,RRci=3] playsound minecraft:entity.pig.death voice @a[c=1]
	$server$clear @a[RRDeadPigPlayer] carrot_on_a_stick
	scoreboard players tag @a[RRDeadPigPlayer] remove RRDeadPigPlayer
	scoreboard players reset @a[RRpc3<=10000] RRpc3
	
//Removal of tags
	scoreboard players tag @a[RRPigPickup] remove RRPigPickup
	scoreboard players tag @a[RRPigDestruct] remove RRPigDestruct

	
//* Gives back pig egg *//
	replaceitem entity @a[RRci=3,team=RRg,RRPigDispenserGive] slot.hotbar.4 EngieItemEggG
	replaceitem entity @a[RRci=3,team=RRb,RRPigDispenserGive] slot.hotbar.4 EngieItemEggB
	scoreboard players tag @a[RRPigDispenserGive] remove RRPigDispenserGive
	
//* Ammo regeneration *//
//	Has to regen cloak, rockets and blindness arrows
	@e[$SA$,type=Pig,RRPigGreen] scoreboard players tag @a[r=$DispenserRadius$,m=2,team=RRg] add RRNearDispenser
	@e[$SA$,type=Pig,RRPigBlue] scoreboard players tag @a[r=$DispenserRadius$,m=2,team=RRb] add RRNearDispenser
	@a[$SA$,m=2,RRci>=2<=3,!RRNearDispenser] RRao = 0
	@a[$SA$,m=2,RRci=0,RRst=0,RRsa>=-1<=279,RRNearDispenser] RRsa + 1
	@a[$SA$,m=2,RRci=2,RRNearDispenser] $server$clear @a[r=0,c=1,RRci=2] ArchItemRocket(-1;0)
	@a[$SA$,m=2,RRci=3,RRNearDispenser] $server$clear @a[r=0,c=1,RRci=3] EngieItemArrow(-1;0)
	
	@a[$SA$,m=2,RRci=2,RRaf<=2,RRao<=99,RRNearDispenser] RRao + 1
	replaceitem entity @a[$SA$,m=2,RRci=2,RRaf=0,RRao=$DispenserRocketDelay$,RRNearDispenser] slot.hotbar.7 ArchItemRocket(1;0)
	$server$give @a[$SA$,m=2,RRci=2,RRaf>=1<=2,RRao=$DispenserRocketDelay$,RRNearDispenser] ArchItemRocket(1;0)
	@a[$SA$,m=2,RRci=2,RRNearDispenser,RRao>=$DispenserRocketDelay$] RRao = 0
	
	@a[$SA$,m=2,RRci=3,RRaf<=1,RRao<=149,RRNearDispenser] RRao + 1
	replaceitem entity @a[$SA$,m=2,RRci=3,RRaf=0,RRao=$DispenserBlindnessDelay$,RRNearDispenser] EngieItemArrow(1;0)
	$server$give @a[$SA$,m=2,RRci=3,RRaf=1,RRao=$DispenserBlindnessDelay$,RRNearDispenser] EngieItemArrow(1;0)
	@a[$SA$,m=2,RRci=3,RRNearDispenser,RRao>=$DispenserBlindnessDelay$] RRao = 0

//>

//* Health regeneration *//
//Cannot use regeneration anywhere else
//	0: reset
//	1: engie regen
//	2: dispenser regen (takes over engie regen ofc)
//	Transfer from 2 to 1 and 1 to 2: does not reset timer
//	RRrt

	effect @a[$SA$,m=2] 10 0 0 true
	@a[$SA$,m=2,RRci=3,team=RRg] @a[r=$EngieRegenRadius$,m=2,team=RRg] RRrt = 1
	@a[$SA$,m=2,RRci=3,team=RRb] @a[r=$EngieRegenRadius$,m=2,team=RRb] RRrt = 1
	@a[RRNearDispenser,m=2] RRrt = 2
	@a[$SA$,m=2,RRrt=0,RRrg>=1] RRrg = 0
	@a[$SA$,m=2,RRrt>=1<=2,RRrg>=0<=SUB($EngieRegenDelay$;1)] RRrg + 1
	effect @a[$SA$,m=2,RRrt=1,RRrg=$EngieRegenDelay$] 10 1 10 true
	effect @a[$SA$,m=2,RRrt=2,RRrg=$DispenserRegenDelay$] 10 1 10 true
	
	@a[$SA$,m=2,RRrt=1,RRrg>=$EngieRegenDelay$] RRrg = 0
	@a[$SA$,m=2,RRrt=2,RRrg>=$DispenserRegenDelay$] RRrg = 0
	@a[RRrt>=1] RRrt = 0
	scoreboard players tag @a[RRNearDispenser] remove RRNearDispenser
	
//* XP effects *//
	$server$xp 1L @a[$SA$,m=2,RRci=3,score_RRam_min=1]
	$server$xp 1 @a[$SA$,m=2,RRci=3,score_RRam_min=1]
	$server$xp 1 @a[$SA$,m=2,RRci=3,score_RRam_min=40]
	$server$xp 1 @a[$SA$,m=2,RRci=3,score_RRam_min=80]
	$server$xp 1 @a[$SA$,m=2,RRci=3,score_RRam_min=120]
	$server$xp 1 @a[$SA$,m=2,RRci=3,score_RRam_min=160]
	$server$xp 1 @a[$SA$,m=2,RRci=3,score_RRam_min=200]
	$server$xp 1 @a[$SA$,m=2,RRci=3,score_RRam_min=240]
	$server$xp 1 @a[$SA$,m=2,RRci=3,score_RRam_min=280]
	
//XP Sound effects
//Plays every second
	@a[$SA$,m=2,RRpt=0,RRci=3,RRam>=1<=319] RRpc4 = RRam
	@a[$SA$,m=2,RRpt=0,RRci=3,RRam>=1<=319] RRpc4 % 20
	@a[$SA$,m=2,RRpt=0,RRci=3,RRpc4=0,RRam>=1<=319] playsound entity.experience_orb.touch voice @a[c=1] ~ ~ ~ 0.5
	@a[m=2,RRci=3,RRpc4<=320] RRpc4 = 0
END

FUNC EngineerPigAstheticsClock
	//Teleporting armor stands. Can be disabled if causing lag
	$server$tp @e[type=ArmorStand,RRASPig] ~ ~ ~ ~2 ~
	@e[$SA$,type=Pig,RRPigGreen,RRns=3] particle mobSpell ~ ~0.9 ~ 0.3 1 0.3 1 0 force
	@e[$SA$,type=Pig,RRPigBlue,RRns=3] particle mobSpell ~ ~0.9 ~ 0.3 0.3 1 1 0 force
	@e[$SA$,type=Pig,RRPigGreen,RRns=3] @a[r=3,m=2,team=RRg] particle mobSpellAmbient ~ ~ ~ 0.2 1 0.2 1 0 force
	@e[$SA$,type=Pig,RRPigBlue,RRns=3] @a[r=3,m=2,team=RRb] particle mobSpellAmbient ~ ~ ~ 0.2 0.2 1 1 0 force
END


FUNC EngineerClock
//* Teleporting *//
	//RRtd
	scoreboard players tag @a[$SA$,m=2,RRci=3] add RREngineerTeleport {ActiveEffects:[{Id:13b,Amplifier:0b}]}
	@a[$SA$,m=2,RRci=3,RREngineerTeleport] RRtd + 0
	@a[$SA$,m=2,RRci=3,RRtd=0] playsound minecraft:weather.rain voice @a[r=5] ~ ~ ~ 2 1.5
	@a[$SA$,m=2,RRci=3,RRtd>=0<=$EngieTeleportDelay$] RRtd + 1
	@a[$SA$,m=2,RRci=3,RRtd>=1<=$EngieTeleportDelay$] particle explode ~ ~1 ~ 0.3 0.6 0.3 0 1 force @a
	$server$tp @a[$SA$,m=2,RRci=3,RRtd=$EngieTeleportDelay$,team=RRg] $SpawnG$
	$server$tp @a[$SA$,m=2,RRci=3,RRtd=$EngieTeleportDelay$,team=RRb] $SpawnB$
	scoreboard players tag @a[$SA$,m=2,RRci=3,RRtd=$EngieTeleportDelay$] add RRTeleportEffect
	scoreboard players tag @a[$SA$,m=2,RRci=3,RRtd=$EngieTeleportDelay$] add RRTarget3
	scoreboard players reset @a[$SA$,m=2,RRci=3,RRtd>=$EngieTeleportDelay$] RRtd
	effect @a[RREngineerTeleport] 13 0 0 true
	scoreboard players tag @a[RREngineerTeleport] remove RREngineerTeleport
	
//* All weakness effects applied here *//
	effect @a[$SA$,m=2,RRWeakness] 18 3 10 true
	effect @a[$SA$,m=2,!RRWeakness] 18 0 0 true
	scoreboard players tag @a[RRWeakness] remove RRWeakness
	
	scoreboard players tag @a[m=2,RRde>=1,RRci=3] add RRTarget3
END

FUNC EngineerEnd
	USE $ObjR$
		RRam
		RRns
		RRue
		RRdp
		RRae
		RRtd
		RRpt
		RRrt
		RRrg
		RRao
		RRpc1
		RRpc2
		RRpc3
		RRpc4
		RRpc5
	UNUSE
	$server$kill @e[RRPigDispenser]
	scoreboard players tag * remove RRDispenserPickup
	scoreboard players tag * remove RRDestructRequest
END

FUNC EngineerGive
	@a[$SA$,m=2,RRTarget3] RRci = 3
	@a[$SA$,m=2,RRTarget3] RRpt + 0
	replaceitem entity @a[$SA$,m=2,RRTarget3,team=RRg] EngieItemHeadG
	replaceitem entity @a[$SA$,m=2,RRTarget3,team=RRb] EngieItemHeadB
	replaceitem entity @a[$SA$,m=2,RRTarget3,team=RRg] EngieItemChestG
	replaceitem entity @a[$SA$,m=2,RRTarget3,team=RRb] EngieItemChestB
	replaceitem entity @a[$SA$,m=2,RRTarget3,team=RRg] EngieItemLegsG
	replaceitem entity @a[$SA$,m=2,RRTarget3,team=RRb] EngieItemLegsB
	replaceitem entity @a[$SA$,m=2,RRTarget3,team=RRg] EngieItemFeetG
	replaceitem entity @a[$SA$,m=2,RRTarget3,team=RRb] EngieItemFeetB
	replaceitem entity @a[$SA$,m=2,RRTarget3] slot.hotbar.0 EngieItemPickaxe
	@a[$SA$,m=2,RRTarget3] replaceitem entity @a[r=0,c=1,m=2,!RRDispenserPickup] slot.hotbar.1 EngieItemBow
	replaceitem entity @a[$SA$,m=2,RRTarget3] slot.hotbar.2 EngieItemPotion
		@a[$SA$,m=2,RRTarget3] RRam + 0
	replaceitem entity @a[$SA$,m=2,RRTarget3,team=RRg,RRpt=0,RRam=0] slot.hotbar.4 EngieItemEggG
	replaceitem entity @a[$SA$,m=2,RRTarget3,team=RRb,RRpt=0,RRam=0] slot.hotbar.4 EngieItemEggB
		scoreboard players reset @a[$SA$,m=2,RRTarget3,RRam=0] RRam
	replaceitem entity @a[$SA$,m=2,RRTarget3,RRpt=2] slot.hotbar.5 EngieItemPickup
	replaceitem entity @a[$SA$,m=2,RRTarget3,RRpt=2] slot.hotbar.6 EngieItemDestruct
	replaceitem entity @a[$SA$,m=2,RRTarget3] slot.hotbar.7 EngieItemArrow(2;0)
	replaceitem entity @a[$SA$,m=2,RRTarget3] ItemRedstone(2)
END

//Pickaxe stats: 1 attack speed, 4.5 attack damage

FUNC EngineerCC
	scoreboard players tag @a[$SA$,m=2,|0|,RRpt=2] add RRDestructRequest
	@a[$SA$,m=2,|0|] RRpt = 0
	@a[$SA$,m=2,|0|] RRue = 0
	scoreboard players reset @a[$SA$,m=2,|0|] RRtd
	scoreboard players tag @a[$SA$,m=2,|0|] remove RRDispenserPickup
END
//>



:ParticleASReset PULSE 16 12 0
Deactivate(ParticleASReset)
//ArmorStandParticle


:MainStart PULSE 16 12 0
Deactivate(MainStart)
GeneralScoreboardStart
StealthStart
WarriorStart
ArcherStart
EngineerStart
TeleportStart
Activate(MainClock)
Activate(DispenserPigAsthetics)


:MainClock CLOCK 16 12 0
GeneralScoreboardClock
StealthCloakClock
StealthClock
WarriorClock
ArcherClock
EngineerClock
EngineerPigClock
TeleportClock
@a[RRki>=1] RRki = 0
@a[RRde>=1] RRde = 0
@a[RRcs>=1] RRcs = 0


:MainEnd PULSE 16 12 0
Deactivate(MainEnd)
GeneralScoreboardEnd
StealthEnd
WarriorEnd
ArcherEnd
EngineerEnd
TeleportEnd


:StealthItems CLOCK 16 12 0
Deactivate(StealthItems)
UniversalGive(RRTarget0)
StealthGive
WarriorCC(RRTarget0)
ArcherCC(RRTarget0)
EngineerCC(RRTarget0)
RemoveTag(RRTarget0)

:WarriorItems CLOCK 16 12 0
Deactivate(WarriorItems)
UniversalGive(RRTarget1)
WarriorGive
StealthCC(RRTarget1)
ArcherCC(RRTarget1)
EngineerCC(RRTarget1)
RemoveTag(RRTarget1)

:ArcherItems CLOCK 16 12 0
Deactivate(ArcherItems)
UniversalGive(RRTarget2)
ArcherGive
StealthCC(RRTarget2)
WarriorCC(RRTarget2)
EngineerCC(RRTarget2)
RemoveTag(RRTarget2)

:EngineerItems CLOCK 16 12 0
Deactivate(EngineerItems)
UniversalGive(RRTarget3)
EngineerGive
StealthCC(RRTarget3)
WarriorCC(RRTarget3)
ArcherCC(RRTarget3)
RemoveTag(RRTarget3)


//:Particle CLOCK 16 12 0
//ParticleClock


:SimpleParticle CLOCK 16 12 0
@e[type=ArmorStand,RRTPOpenP] particle happyVillager ~ ~1.25 ~ 0.3 1.5 0.3 0 2 force


:DispenserPigAsthetics CLOCK 16 12 0
EngineerPigAstheticsClock
